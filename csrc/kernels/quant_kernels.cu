// SPDX-License-Identifier: MIT
// Copyright (C) 2024-2026, Advanced Micro Devices, Inc. All rights reserved.

// This translation unit is torch-free: define AITER_NO_TORCH_TYPES before any
// aiter header so aiter_opus_plus.h does not pull in the c10 half/bfloat16
// headers. The kernels use aiter::hip2opus + the _rmTorch dispatch macros, never
// the t2opus<c10::*> specializations, so nothing here needs torch/ATen/c10.
#define AITER_NO_TORCH_TYPES
#include "aiter_hip_common.h"
#include "aiter_dispatch.h"
#include "aiter_opus_plus.h"
#include "aiter_stream.h"
#include "gemm_dispatch_utils.h"
#include "quant.h"
#include "mx_quant_utils.h"
#include "rocprim/rocprim.hpp"
#include <cstdlib>


const int32_t BlockSize           = 256;
const int32_t groupQuantBlockSize = 64;

namespace aiter {

// Host-side twin of the kernel's kTunedForThisArch. Cached: get_gpu_arch() queries the
// device properties, and this sits on the launch path of every quant call.
static inline bool dyn_gq_tuned_arch()
{
    static const bool tuned = (get_gpu_arch() == "gfx1250");
    return tuned;
}

// gfx1250 tuning. All three are swept values; the sweeps are at the use sites.
// The 256-thread block and TDM staging are mutually exclusive, and TDM wins where it
// applies: a wider block scales the staged tile with it (16 KiB per ring slot instead of
// 4), so the two together cost 22% at T=8192. Below the staging threshold the wide block
// is worth 1-6% from T=512 up, and 2.7% NEGATIVE at T=128. Denominator 4 = a quarter of a
// SIMD's worth of blocks, which puts the switch between T=128 and T=512.
constexpr int kDynGqWideBlkPerSimdDenom = 4;
// Blocks/SIMD below which TDM staging is skipped. At blk=64 this counts
// rows*scaleN/32, so T=4096 sits at 7168 and T=8192 at 14336: 8 puts the switch between
// them, which is where staging starts paying. It measured 10.18 vs 10.02 us at T=4096
// (slightly harmful) against ~15% faster at T=16384. Below this the block widens to 256
// instead -- the two are mutually exclusive, see kDynGqWideBlkPerSimdDenom.
constexpr int kDynGqTdmMinBlkPerSimd  = 8;
constexpr int kDynGqTdmKPT            = 2;  // staged steps per block

// emit_e8m0_scale = false (default): legacy behaviour — fp4 outputs an e8m0
// byte scale, fp8 / i8 output a continuous fp32 per-group scale.
//
// emit_e8m0_scale = true (opt-in for fp8): compute a power-of-2 per-group
// scale via f32_to_e8m0_scale and write a single E8M0 byte per group,
// matching the fp4 byte layout. Used by the MXFP8 "split" path
// `per_1x32_mx_quant_hip(quant_dtype=fp8, scale_type=fp8_e8m0)` so the
// produced byte scale is directly consumable by `mxfp4_moe_sort_hip` /
// MXFP8 GEMM kernels without a post-hoc fp32 -> e8m0 conversion.
// Every TUNING choice below is gated on gfx1250: each was swept there and leans on
// something arch-specific (wave32, b128 as the widest per-lane access). Nothing was
// measured on gfx950, so that target keeps the shape it had before.
template <typename DTYPE_I, typename DTYPE_O, int thread_data_size = 32, int32_t group_size = 128, bool shuffle_scale = true, int32_t block_size = 64, bool emit_e8m0_scale = false, bool enable_tdm = false, int tdm_tile_rows = 0, bool packed_bf16_amax = false, bool full_group_stores = false, bool wide_group_stores = false, bool grid_2d = false, bool direct_prefetch = false>
__global__ void __launch_bounds__(block_size)
dynamic_per_group_scaled_quant_kernel(DTYPE_O* __restrict__ out,
                                      float* __restrict__ scale,
                                      DTYPE_I const* __restrict__ input,
                                      float const* __restrict__ scale_ub,
                                      int64_t ori_rows,
                                      int32_t ori_cols,
                                      int32_t ori_row_stride,
                                      int64_t oob_size,
                                      int32_t const* __restrict__ num_rows = nullptr,
                                      const int32_t num_cols_factor        = 1)
{
    // The 2D path is selected only for dense input without dynamic row counts.
    // This invariant is independent of the numeric M/K values.
    if constexpr(grid_2d)
    {
        num_rows = nullptr;
        ori_row_stride = ori_cols;
        oob_size = ori_rows * static_cast<int64_t>(ori_cols);
    }
    static_assert(!emit_e8m0_scale
                      || std::is_same_v<DTYPE_O, opus::fp4_t>
                      || std::is_same_v<DTYPE_O, opus::fp8_t>,
                  "emit_e8m0_scale is only valid for fp4 / fp8 outputs");

    // fp4 always emits e8m0 byte scale (no fp32-scale variant exists today);
    // fp8 emits e8m0 byte scale iff caller opted in via emit_e8m0_scale.
    static constexpr bool use_e8m0_scale =
        std::is_same_v<DTYPE_O, opus::fp4_t> || emit_e8m0_scale;

    if(num_rows != nullptr)
    {
        ori_rows = static_cast<int64_t>(*num_rows) * num_cols_factor;
    }
    static constexpr int num_thread_per_group = group_size / thread_data_size;
    const int lane_in_group  = threadIdx.x % num_thread_per_group;
    // Reused at the end as the scale-store index, which the shuffled layouts rewrite.
    int64_t groupId          = static_cast<int64_t>(blockIdx.x) * block_size / num_thread_per_group
                               + threadIdx.x / num_thread_per_group;
    int32_t scaleN           = ori_cols / group_size;
    // Shuffle tiles e8m0 bytes 8-wide along scaleN for the MX hardware
    // scale-load layout (group_size == 32 only); group_size == 128 shuffle
    // is a plain transpose and needs no padding.
    int32_t scaleN_pad       = (use_e8m0_scale && shuffle_scale && group_size == 32)
                                   ? (((scaleN + 7) / 8) * 8)
                                   : scaleN;
    // Group ordering follows whichever stream is the scattered one. The transposed layout
    // writes `y * ori_rows + x`, so under row-major order consecutive lanes land ori_rows
    // apart -- one byte per cache line, measured at 4.8 us (15% of the kernel) to move
    // 0.9 MB. Column-major makes that store contiguous and strides the data reads instead,
    // but each group still reads one contiguous 256B run. Net: 36.3 -> 32.0 us.
#if defined(__gfx1250__)
    static constexpr bool kTunedForThisArch = true;
#else
    static constexpr bool kTunedForThisArch = false;
#endif
    static constexpr bool kColumnMajorGroups =
        kTunedForThisArch && use_e8m0_scale && shuffle_scale && group_size == 128;

    // TDM staging parameters. Only the column-major path qualifies: it is the one whose
    // wave-sized run of groups forms a 2D tile (consecutive rows at one y). Row-major
    // groups walk y, which would make a wave's slice a strided set of partial rows.
    static constexpr int kGroupsPerWave     = WARP_SIZE / num_thread_per_group;
    static constexpr int kTdmWaves          = block_size / WARP_SIZE;
    // Tiles a wave stages before consuming any. Swept at [16384, 7168] e8m0+transposed:
    //     KPT   1(off)    2      3      4      8
    //     us     27.0   25.96  29.3   32.1   54.8
    // The body is unrolled once per tile, so deeper only grows the instruction count
    // (344 -> 1795 at 4) once two loads are already in flight.
    static constexpr int kTdmKPT            = kDynGqTdmKPT;
    // 3 is the hardware's in-flight tensor-op limit per wave (opus.hpp), but kTdmKPT steps
    // never leave more than that outstanding, so a slot beyond kTdmKPT is LDS nobody
    // writes. Dropping it at KPT 2 freed 4 KiB/block for -3.6% (24.40 -> 23.53 us) with
    // the body byte-identical -- the LDS request is the only thing that changes.
    static constexpr int kTdmRing           = kTdmKPT < 3 ? kTdmKPT : 3;
    static constexpr int kTdmGroupsPerStep  = kTdmWaves * kGroupsPerWave;
    static constexpr int kTdmGroupsPerBlock = kTdmGroupsPerStep * kTdmKPT;
    static constexpr int kTdmSlotElems      = kTdmGroupsPerStep * group_size;
    static constexpr int kTdmLdsWaves       = 1;   // one shared staging area per block
    // The host owns this decision: the two paths need different grids, and deciding it
    // here would let the host size for staging while the kernel fell back, leaving half
    // the groups unvisited.
    static constexpr bool kUseTdmShape =
        enable_tdm && kColumnMajorGroups && kTdmKPT > 1 && kTdmWaves >= 1 &&
        (block_size % WARP_SIZE) == 0;

#if defined(__gfx1250__)
    // One descriptor per BLOCK: the tile is every wave's rows at once. tensorcnt retires
    // per wave, so only the issuer can wait and the rest need a barrier -- half the
    // descriptors and a 2x wider tile against two barriers per stage.
    static constexpr bool kTiledTdm = kUseTdmShape && tdm_tile_rows > 0;
    static constexpr int kTileRows = kTiledTdm ? tdm_tile_rows : kTdmGroupsPerStep;
    static_assert(kTdmGroupsPerStep % kTileRows == 0);
    using TdmWindow = opus::tdm<DTYPE_I,
        opus::seq<group_size * (kTdmGroupsPerStep / kTileRows), kTileRows>>;
#endif

    // All four lanes of a group share a group id, hence the same (x, y), so the DPP reduce
    // below never reads across an active/inactive lane boundary.
    auto resolve = [&](int64_t gid, int64_t& gx, int32_t& gy) -> bool {
#if defined(__gfx1250__)
        if constexpr(grid_2d)
        {
            static_assert(kColumnMajorGroups && group_size == 128);
            if constexpr(wide_group_stores)
            {
                constexpr int kTileCols=kTdmGroupsPerBlock/kTileRows;
                const int local=static_cast<int>(gid%kTdmGroupsPerBlock);
                gx=static_cast<int64_t>(blockIdx.y)*kTileRows+(local%kTdmGroupsPerStep)/2;
                gy=static_cast<int>(blockIdx.x)*kTileCols+(local/kTdmGroupsPerStep)*2+local%2;
            }
            else
            {
                gx=gid;
                gy=static_cast<int>(blockIdx.y);
            }
        }
        else
#endif
        if constexpr(kColumnMajorGroups)
        {
#if defined(__gfx1250__)
            if constexpr(kTiledTdm)
            {
                constexpr int kTileCols = kTdmGroupsPerBlock / kTileRows;
                const int64_t tile_id = gid / kTdmGroupsPerBlock;
                const int local = static_cast<int>(gid % kTdmGroupsPerBlock);
                const int tiles_per_row = scaleN / kTileCols;
                if constexpr(wide_group_stores)
                {
                    static_assert(kTdmGroupsPerStep == 2*kTileRows && kTdmKPT == 2);
                    // Neighboring lanes' groups cover neighboring columns of
                    // the same row, allowing 256B contiguous regular stores.
                    gx=(tile_id/tiles_per_row)*kTileRows+(local%kTdmGroupsPerStep)/2;
                    gy=(tile_id%tiles_per_row)*kTileCols+(local/kTdmGroupsPerStep)*2+local%2;
                }
                else
                {
                    gx = (tile_id / tiles_per_row) * kTileRows + local % kTileRows;
                    gy = static_cast<int32_t>(tile_id % tiles_per_row) * kTileCols + local / kTileRows;
                }
            }
            else
#endif
            {
                gx = gid % ori_rows;
                gy = static_cast<int32_t>(gid / ori_rows);
            }
        }
        else
        {
            gx = gid / scaleN_pad;
            gy = static_cast<int32_t>(gid % scaleN_pad);
        }
        if constexpr(use_e8m0_scale)
            return gx < ori_rows && gy < scaleN;
        else
            return gx < ori_rows;
    };
    int64_t x;
    int32_t y;

    using vec_i = opus::vector_t<DTYPE_I, thread_data_size>;
    static constexpr int32_t vec_size_o =
        std::is_same_v<DTYPE_O, opus::fp4_t> ? thread_data_size / 2 : thread_data_size;
    // The non-e8m0 (continuous fp32-scale) path uses the exact 1/DTYPE_MAX
    // divisor. The e8m0 path instead derives a power-of-2 scale via
    // fp_f32_to_e8m0_scale<> below (which folds in / max_pos), so it does not
    // use this divisor.
    const float inverted_DTYPE_MAX =
        (1. / static_cast<float>(opus::finfo<DTYPE_O>::max()));

    // How a group's elements split across its threads. The obvious contiguous 64B run per
    // thread strides a load's lanes 64B apart, so nothing merges; two 32B chunks (t and
    // t+ntpg) halve both strides and make the store contiguous across a group. 16B chunks
    // would perfectly coalesce the loads but shrink the fp8 store to 8B per lane, and that
    // trade measured worse. A thread's elements are no longer contiguous, which nothing
    // downstream cares about: amax is order-independent and store_vector's interleave mode
    // lays the output back down in this pattern.
    static constexpr int kChunkElems = 32 / static_cast<int>(sizeof(DTYPE_I));
    static constexpr int kChunks     = kChunkElems ? thread_data_size / kChunkElems : 0;
    static constexpr bool kInterleavedChunks =
        kTunedForThisArch && !std::is_same_v<DTYPE_O, opus::fp4_t> && sizeof(DTYPE_I) == 2 &&
        kChunkElems > 0 && thread_data_size % kChunkElems == 0 && kChunks > 1;

    using chunk_t = opus::vector_t<DTYPE_I, kChunkElems>;

    // Gather this thread's slice of one group out of a contiguous base pointer, which is
    // either the group's address in global memory or its staging area in LDS.
    auto gather = [&](DTYPE_I const* gbase) -> vec_i {
        vec_i td;
        if constexpr(kInterleavedChunks)
        {
            auto const* chunks = reinterpret_cast<chunk_t const*>(gbase);
            opus::static_for<kChunks>([&](auto i) {
                chunk_t c = chunks[lane_in_group + i.value * num_thread_per_group];
                opus::static_for<kChunkElems>(
                    [&](auto j) { td[i.value * kChunkElems + j.value] = c[j.value]; });
            });
        }
        else
        {
            td = reinterpret_cast<vec_i const*>(gbase)[lane_in_group];
        }
        return td;
    };

    using saved_scale_t = std::conditional_t<use_e8m0_scale, uint8_t, float>;
    struct PendingGroupStore
    {
        opus::vector_t<uint8_t, 16> even_bytes;
        opus::vector_t<uint8_t, 16> odd_bytes;
        int32_t offset;
        saved_scale_t* scale_dst;
        saved_scale_t scale_val;
    };
    auto emit_group = [&](const PendingGroupStore& pending) {
        auto buffer_o = opus::make_gmem<uint8_t>(reinterpret_cast<uint8_t*>(out), oob_size);
        opus::store<16>(buffer_o, pending.even_bytes, pending.offset, 0, opus::number<0>{});
        asm volatile("s_nop 0");
        opus::store<16>(buffer_o, pending.odd_bytes, pending.offset + ori_cols, 0, opus::number<0>{});
        asm volatile("s_nop 0");
        if(pending.scale_dst != nullptr)
            *pending.scale_dst = pending.scale_val;
    };

    auto process = [&](vec_i thread_data, int64_t x, int32_t y, int64_t groupId) {
        float absMax = 1e-10f;
        if constexpr(packed_bf16_amax && kTunedForThisArch)
        {
            static_assert(std::is_same_v<DTYPE_I, opus::bf16_t> && thread_data_size % 2 == 0);
            const auto packed_data = __builtin_bit_cast(
                opus::vector_t<uint32_t, thread_data_size / 2>, thread_data);
            uint32_t packed_max = 0;
#pragma unroll
            for(int j = 0; j < thread_data_size / 2; ++j)
            {
                const uint32_t pair = packed_data[j] & 0x7fff7fffU;
                asm("v_pk_max_u16 %0, %1, %2" : "=v"(packed_max) : "v"(packed_max), "v"(pair));
            }
            const uint32_t magnitude = opus::max(packed_max & 0xffffU, packed_max >> 16);
            if(magnitude > 0x7f80U)
            {
                // Unsigned ordering puts NaNs above infinity. Preserve the original
                // reduction semantics when a lane contains a NaN.
                for(size_t j = 0; j < thread_data_size; ++j)
                    absMax = max(absMax, abs(static_cast<float>(thread_data[j])));
            }
            else
                absMax = max(absMax, __builtin_bit_cast(float, magnitude << 16));
        }
        else
        {
            for(size_t j = 0; j < thread_data_size; ++j)
                absMax = max(absMax, abs(static_cast<float>(thread_data[j])));
        }
        absMax = multithread_reduce(absMax, aiter::Max(), num_thread_per_group);
        // `v_cvt_scalef32_pk8_fp8_bf16` does not saturate -- past 464, the midpoint between fp8
        // e4m3's top two steps, it rounds into the NaN encoding -- where the software med3 path
        // clamps. Cap amax so an inf group still gets a finite scale, then saturate below.
        // (kStoreTakesDivisor is declared here because the cap has to precede the scale.)
        static constexpr bool kStoreTakesDivisor =
            use_e8m0_scale && std::is_same_v<DTYPE_O, opus::fp8_t> &&
            std::is_same_v<DTYPE_I, opus::bf16_t> && (thread_data_size % 8 == 0);
        static constexpr bool kHwConvertDiv = kTunedForThisArch && kStoreTakesDivisor;
        static constexpr bool kScaleMayClip =
            aiter::kDefaultMxScaleRoundMode == aiter::MxScaleRoundMode::RoundDown ||
            aiter::kDefaultMxScaleRoundMode == aiter::MxScaleRoundMode::Even;
        bool degenerate_group = false;
        if constexpr(kHwConvertDiv)
        {
            degenerate_group = !(absMax < __builtin_inff());
            absMax           = fminf(absMax, 448.0f * 0x1.0p119f);
        }

        // MX e8m0 path: use the project-wide default round mode
        // (``kDefaultMxScaleRoundMode``, currently RoundUp = NV / DSv4 RCEIL).
        // The helper returns the dequant scale (e.g. ceil_pow2(amax/max_pos))
        // directly, so the (>>23)&0xFF extraction yields the e8m0 byte. fp4
        // always e8m0; fp8 only when emit_e8m0_scale (use_e8m0_scale gates this).
        // rmode is shared across fp4/fp8; only the dtype constant differs.
        float inverted_scale;
        if constexpr (use_e8m0_scale)
        {
            constexpr aiter::MxDtype kMxDtype =
                std::is_same_v<DTYPE_O, opus::fp4_t>
                    ? aiter::MxDtype::FP4_E2M1
#if defined(__gfx942__)
                    : aiter::MxDtype::FP8_E4M3_FNUZ;
#else
                    : aiter::MxDtype::FP8_E4M3;
#endif
            inverted_scale =
                aiter::fp_f32_to_e8m0_scale<aiter::kDefaultMxScaleRoundMode, kMxDtype>(absMax);
        }
        else
        {
            inverted_scale = absMax * inverted_DTYPE_MAX;
        }
        // Interleaved layout: this thread's first chunk sits at lane * kChunkElems, and
        // store_vector's interleave mode strides the rest by num_thread_per_group chunks.
        static constexpr int kLaneStride = kInterleavedChunks ? kChunkElems : vec_size_o;
        // The output is row-major regardless of the order in which groups are visited.
        // Keep row and in-row offsets separate so tensors beyond a global descriptor's
        // 32-bit byte reach can use one row as the descriptor range.
        const int64_t out_row_offset =
            std::is_same_v<DTYPE_O, opus::fp4_t> ? x * ori_cols / 2 : x * ori_cols;
        const int32_t out_thread_offset =
            (std::is_same_v<DTYPE_O, opus::fp4_t> ? y * group_size / 2
                                                  : y * group_size) +
            lane_in_group * kLaneStride;
        const int64_t row_offset = out_row_offset + out_thread_offset;
        // The scale write happens at the end of the kernel, but its address and value are
        // resolved HERE. Left in the tail, that arithmetic reused the data stores' VGPRs and
        // forced an `s_wait_xcnt 0x0` guarding every outstanding VMEM -- an ATT capture
        // charged 18% of kernel latency to that one wait. Costs three VGPRs held live.
        // A null `scale_dst` doubles as the "not the group's first lane" predicate.
        const float row_scale = inverted_scale;
        using scale_elem_t    = std::conditional_t<use_e8m0_scale, uint8_t, float>;
        scale_elem_t* scale_dst = nullptr;
        scale_elem_t  scale_val{};
        if(lane_in_group == 0)
        {
            int64_t scale_idx = groupId;
            if constexpr(shuffle_scale)
            {
                if constexpr(use_e8m0_scale && group_size == 32)
                    scale_idx = aiter::mx_scale_shuffle_idx(scaleN_pad, static_cast<int>(x), y);
                else
                    scale_idx = y * ori_rows + x;
            }
            if constexpr(use_e8m0_scale)
            {
                scale_dst = reinterpret_cast<uint8_t*>(scale) + scale_idx;
                scale_val = static_cast<uint8_t>(
                    (__builtin_bit_cast(uint32_t, row_scale) >> 23) & 0b11111111);
            }
            else
            {
                scale_dst = scale + scale_idx;
                scale_val = row_scale;
            }
        }
        // Which form of the scale the store path consumes: fp4 and kStoreTakesDivisor take
        // `row_scale` directly (the latter lets gfx1250 use `v_cvt_scalef32_pk8_fp8_bf16`,
        // 80 instructions -> 4 per 32 elements); everything else needs the reciprocal.
        //
        // use_e8m0_scale is a correctness precondition, not a tuning gate: that convert reads
        // its scale as an MX E8M0 factor, keeping the exponent and discarding the mantissa, so
        // it is exact only for a power-of-two scale. The continuous one measured 1.79x off.
        // Conversely the reciprocal must gate on the store form, not on use_e8m0_scale --
        // gating it that way once skipped it on the software path (`split_elem_err ~ 100%`).
        if constexpr(!std::is_same_v<DTYPE_O, opus::fp4_t> && !kStoreTakesDivisor)
        {
            inverted_scale = 1.0f / inverted_scale;
        }

        // Only RoundDown / Even can floor the scale enough for finite data to overflow, so under
        // the shipped RoundUp this folds to `if(degenerate_group)` -- never taken, and free.
        // Compares rather than min/max-es: both tests are false for NaN, so NaN stays NaN.
        if constexpr(kHwConvertDiv)
        {
            if(kScaleMayClip || degenerate_group)
            {
                const float hi = 448.0f * inverted_scale;
                for(size_t j = 0; j < thread_data_size; j++)
                {
                    const float v = static_cast<float>(thread_data[j]);
                    if(v > hi)
                        thread_data[j] = static_cast<DTYPE_I>(hi);
                    if(v < -hi)
                        thread_data[j] = static_cast<DTYPE_I>(-hi);
                }
            }
        }

        if constexpr(wide_group_stores && kTunedForThisArch)
        {
            // Regular stores only. Keep cache-policy experiments out of this path.
            static_assert(thread_data_size==32 && group_size==128);
            const auto converted=scaled_cast_div<opus::fp8_t>(thread_data,inverted_scale);
            const auto words=__builtin_bit_cast(opus::vector_t<uint32_t,8>,converted);
            opus::vector_t<uint32_t,4> first,second;
            const int lane=threadIdx.x%32;
            const int j=lane%16;
            const int src=(lane & 16)+(j/8)*4+(j%4);
            opus::static_for<4>([&](auto i) {
                const uint32_t a=__shfl(words[i.value],src,32);
                const uint32_t b=__shfl(words[i.value+4],src,32);
                const uint32_t c=__shfl(words[i.value],src+8,32);
                const uint32_t d=__shfl(words[i.value+4],src+8,32);
                first[i.value]=(j & 4) ? b : a;
                second[i.value]=(j & 4) ? d : c;
            });
            const int base_x=x-((lane>>3)&1);
            const int base_y=y-((lane>>2)&1);
            const int offset=base_x*ori_cols+base_y*128+j*16;
            auto buffer_o=opus::make_gmem<uint8_t>(reinterpret_cast<uint8_t*>(out),oob_size);
            opus::store<16>(buffer_o,__builtin_bit_cast(opus::vector_t<uint8_t,16>,first),
                            offset,0,opus::number<0>{});
            opus::store<16>(buffer_o,__builtin_bit_cast(opus::vector_t<uint8_t,16>,second),
                            offset+ori_cols,0,opus::number<0>{});

        }
        else if constexpr(full_group_stores && kTunedForThisArch)
        {
            static_assert(thread_data_size == 32 && group_size == 128 &&
                          std::is_same_v<DTYPE_O, opus::fp8_t>);
            // Only selected for full waves with neighboring groups at consecutive x.
            // Each store now covers a complete 128B group across eight lanes.
            const auto converted = scaled_cast_div<opus::fp8_t>(thread_data, inverted_scale);
            const auto words = __builtin_bit_cast(opus::vector_t<uint32_t,8>, converted);
            opus::vector_t<uint32_t,4> even_words, odd_words;
            const bool odd = (threadIdx.x & 4) != 0;
            opus::static_for<4>([&](auto i) {
                const uint32_t lo = words[i.value];
                const uint32_t hi = words[i.value+4];
                const uint32_t partner_lo = __shfl_xor(lo,4,32);
                const uint32_t partner_hi = __shfl_xor(hi,4,32);
                even_words[i.value] = odd ? partner_hi : lo;
                odd_words[i.value] = odd ? hi : partner_lo;
            });
            auto buffer_o = opus::make_gmem<uint8_t>(reinterpret_cast<uint8_t*>(out),oob_size);
            const int32_t offset0 = static_cast<int32_t>((x-static_cast<int>(odd))*ori_cols +
                                        y*128 + lane_in_group*16 + (odd ? 64 : 0));
            const auto even_bytes = __builtin_bit_cast(opus::vector_t<uint8_t,16>,even_words);
            const auto odd_bytes = __builtin_bit_cast(opus::vector_t<uint8_t,16>,odd_words);
            if constexpr(direct_prefetch)
            {
                // Keep both batches' converted and exchanged payloads ready before writes.
                return PendingGroupStore{even_bytes, odd_bytes, offset0, scale_dst, scale_val};
            }
            else
            {
                opus::store<16>(buffer_o, even_bytes, offset0, 0, opus::number<0>{});
                asm volatile("s_nop 0");
                opus::store<16>(buffer_o, odd_bytes, offset0 + ori_cols, 0, opus::number<0>{});
                asm volatile("s_nop 0");
            }
        }
        else
        {
            using DTYPE_STORE = std::conditional_t<std::is_same_v<DTYPE_O, opus::fp4_t>, uint8_t, DTYPE_O>;
            auto* out_ptr     = reinterpret_cast<DTYPE_STORE*>(out);
            auto store_output = [&](auto& buffer_o, int64_t offset) __attribute__((always_inline)) {
                if constexpr(kInterleavedChunks)
                {
                    store_vector<DTYPE_STORE, DTYPE_I, thread_data_size, RT, true,
                                 num_thread_per_group, kChunks, DTYPE_O, kStoreTakesDivisor>(
                        buffer_o, thread_data, offset, inverted_scale);
                }
                else
                {
                    store_vector<DTYPE_STORE, DTYPE_I, thread_data_size, RT, false, WARP_SIZE, 1,
                                 DTYPE_O, kStoreTakesDivisor>(
                        buffer_o, thread_data, offset, inverted_scale);
                }
            };

            // Buffer resources expose a 32-bit byte range. For larger outputs, rebase the
            // descriptor to this row and retain the optimized interleaved store mapping.
            constexpr int64_t kDescriptorReach = (int64_t{1} << 32) - 1;
            if(oob_size <= kDescriptorReach)
            {
                auto buffer_o = opus::make_gmem<DTYPE_STORE>(out_ptr, oob_size);
                store_output(buffer_o, row_offset);
            }
            else
            {
                const int64_t out_row_elems =
                    std::is_same_v<DTYPE_O, opus::fp4_t> ? ori_cols / 2 : ori_cols;
                auto buffer_o = opus::make_gmem<DTYPE_STORE>(
                    out_ptr + out_row_offset, out_row_elems * sizeof(DTYPE_STORE));
                store_output(buffer_o, out_thread_offset);
            }

        }

        // Scale write, deferred to last on purpose: address and value were resolved above, so
        // this is a bare store with no arithmetic after it competing for the data stores'
        // registers. See the note at `scale_dst`.
        if(scale_dst != nullptr)
        {
            *scale_dst = scale_val;
        }
    };   // process

#if defined(__gfx1250__)
    if constexpr(kUseTdmShape)
    {
        // Wave 0 stages the block's groups into shared LDS. It waits on tensorcnt;
        // the following block barrier makes each tile available to all waves.
        //
        // The tile is why this works: under column-major order a wave's kGroupsPerWave
        // groups are that many CONSECUTIVE rows at one y, a plain 2D region one descriptor
        // can move. The hardware then picks transaction sizes instead of us paying the 50%
        // coverage of a b128 whose lanes sit 32B apart.
        __shared__ DTYPE_I tdm_lds[kTdmLdsWaves * kTdmRing * kTdmSlotElems];

        const int wave_id  = threadIdx.x / WARP_SIZE;
        const int lane_grp = (threadIdx.x % WARP_SIZE) / num_thread_per_group;
        DTYPE_I* my_lds    = tdm_lds;
        const int slot_grp = wave_id * kGroupsPerWave + lane_grp;   // group within the tile
        const bool issuer  = (wave_id == 0);

        // Tile t of this wave starts at group base + t * kTdmGroupsPerStep.
        const int64_t block_g0 = static_cast<int64_t>(blockIdx.x) * kTdmGroupsPerBlock;
        const int64_t win_g0  = block_g0;                       // one window for the block
        const int64_t wave_g0 = block_g0 + wave_id * kGroupsPerWave;
        int64_t x0;
        int32_t y0;
        // The launch grid retains the unstaged span. Empty blocks must exit before
        // issuing zero-extent tensor loads and waiting at the block barriers.
        // win_g0 is block-uniform, so all waves take the same exit.
        if(!resolve(win_g0, x0, y0))
            return;

        auto w = opus::make_tdm<TdmWindow>(
            static_cast<u32_t>(reinterpret_cast<u64_t>(my_lds)), input,
            static_cast<u32_t>(ori_cols), static_cast<u32_t>(ori_rows),
            static_cast<u64_t>(ori_row_stride),
            static_cast<u32_t>(y0 * group_size), static_cast<u32_t>(x0));

        // Prime the ring, then for each tile wait only for ITS load. The count left
        // outstanding is what is still in flight behind it -- waiting on <0> instead
        // serialises the ring and gives back most of the gain.
        if(issuer)
        {
            opus::static_for<kTdmRing < kTdmKPT ? kTdmRing : kTdmKPT>([&](auto i) {
                if constexpr(i.value > 0) w.move(kTiledTdm ? group_size * (kTdmGroupsPerStep / kTileRows) : 0,
                           kTiledTdm ? 0 : kTdmGroupsPerStep);
                w.async_load(static_cast<u32_t>((i.value % kTdmRing) * kTdmSlotElems));
            });
        }

        opus::static_for<kTdmKPT>([&](auto k) {
            constexpr int issued    = (k.value + kTdmRing) < kTdmKPT ? (k.value + kTdmRing)
                                                                     : kTdmKPT;
            constexpr int remaining = issued - (k.value + 1);
            if(issuer)
                opus::s_wait_tensorcnt<(remaining < 0 ? 0 : remaining)>();
            __builtin_amdgcn_s_barrier();   // publish the tile to the non-issuing waves

            const int64_t gid = wave_g0 + static_cast<int64_t>(k.value) * kTdmGroupsPerStep
                                + lane_grp;
            int64_t gx;
            int32_t gy;
            if(resolve(gid, gx, gy))
            {
                DTYPE_I const* slot = my_lds + (k.value % kTdmRing) * kTdmSlotElems
                                     + (wide_group_stores ? slot_grp :
                                        (kTiledTdm ? (slot_grp % kTileRows) * (kTdmGroupsPerStep / kTileRows)
                                                   + slot_grp / kTileRows
                                                : slot_grp)) * group_size;
                process(gather(slot), gx, gy, gid);
            }

            if constexpr(k.value + kTdmRing < kTdmKPT)
            {
                // The refill lands in the slot this iteration just read, and the compiler
                // does not model the async DMA as aliasing the ds_reads above -- without
                // this barrier the refill can overwrite the slot while they are still
                // outstanding.
                opus::s_wait_dscnt<0>();
                __builtin_amdgcn_s_barrier();   // all waves done reading before the refill
                if(issuer)
                { w.move(kTiledTdm ? group_size * (kTdmGroupsPerStep / kTileRows) : 0,
                           kTiledTdm ? 0 : kTdmGroupsPerStep); w.async_load(
                    static_cast<u32_t>(((k.value + kTdmRing) % kTdmRing) * kTdmSlotElems)); }
            }
        });
        return;
    }
#endif

    if constexpr(direct_prefetch && kTunedForThisArch)
    {
        static_assert(grid_2d && !enable_tdm && full_group_stores && !wide_group_stores);
        static_assert(thread_data_size == 32 && group_size == 128 && shuffle_scale && emit_e8m0_scale);
        // Host dispatch guarantees complete row tiles and dense input. M/K remain runtime.
        // Load both row spans before processing, overlapping independent memory requests.
        constexpr int rows_per_span = block_size / num_thread_per_group;
        const int64_t x0 = static_cast<int64_t>(blockIdx.x) * (2 * rows_per_span)
                           + threadIdx.x / num_thread_per_group;
        const int64_t x1 = x0 + rows_per_span;
        const int y0 = static_cast<int>(blockIdx.y);
        const vec_i data0 = gather(input + x0 * ori_row_stride + y0 * group_size);
        const vec_i data1 = gather(input + x1 * ori_row_stride + y0 * group_size);
        asm volatile("" ::: "memory"); // Compiler ordering only; no GPU barrier.
        const auto pending0 = process(data0, x0, y0, 0);
        const auto pending1 = process(data1, x1, y0, 0);
        emit_group(pending0);
        emit_group(pending1);
    }
    else
    {
        if(!resolve(groupId, x, y))
            return;
        process(gather(input + x * ori_row_stride + y * group_size), x, y, groupId);
    }
}

__global__ void initializeScale(float *d_data, int size, float value)
{
    int idx = blockIdx.x * blockDim.x + threadIdx.x;
    if (idx < size)
    {
        d_data[idx] = value;
    }
}

template <typename DTYPE_I, typename DTYPE_O, int thread_data_size = 16>
__device__ std::tuple<float, DTYPE_I*> data_to_per_row_scale(const DTYPE_I* __restrict__ input,
                                                             const int32_t cols)
{
    static constexpr int32_t vec_size_i =
        thread_data_size == 0 ? 16 / sizeof(DTYPE_O) : thread_data_size;
    static constexpr int32_t vec_size_o =
        std::is_same_v<DTYPE_O, opus::fp4_t> ? vec_size_i / 2 : vec_size_i;
    static constexpr int32_t load_chunk_bytes = sizeof(DTYPE_I) * vec_size_i % 16 == 0 ? 16 : (sizeof(DTYPE_I) * vec_size_i % 8 == 0 ? 8 : 4);
    using vec_i = opus::vector_t<DTYPE_I, vec_size_i>;
    const float inverted_DTYPE_MAX =
        (1. / static_cast<float>(opus::finfo<DTYPE_O>::max()));

    const int64_t row_offset        = blockIdx.x * cols;
    auto const* ptr_i               = reinterpret_cast<DTYPE_I const*>(input + row_offset);
    auto const* input_vecs          = reinterpret_cast<vec_i const*>(ptr_i);
    static constexpr int32_t ooba_i = 4 / sizeof(DTYPE_I);
    const int32_t oob_i             = (cols + ooba_i - 1) / ooba_i * ooba_i;
    auto buffer_i = opus::make_gmem<DTYPE_I>(ptr_i, oob_i * sizeof(DTYPE_I));

    // double load core loop start
    const int32_t num_elems_tail = cols % vec_size_i;
    const int32_t num_vecs       = (cols + vec_size_i - 1) / vec_size_i;

    vec_i vec_cur;
    size_t vec_idx    = threadIdx.x;
    size_t vec_stride = BlockSize;
    if(vec_idx < num_vecs)
    {
        vec_cur = load_vector_nbytes<DTYPE_I, vec_size_i, load_chunk_bytes>(buffer_i, vec_idx * vec_size_i);
    }

    float absMax = 0.f;
    if constexpr(thread_data_size == 0)
    {
        vec_i vec_nxt;
        for(vec_idx += vec_stride; vec_idx < num_vecs; vec_idx += vec_stride)
        {
            vec_nxt = load_vector_nbytes<DTYPE_I, vec_size_i, load_chunk_bytes>(buffer_i, vec_idx * vec_size_i);
            for(size_t j = 0; j < vec_size_i; j++)
            {
                absMax = max(absMax, abs(static_cast<float>(vec_cur[j])));
            }
            vec_cur = vec_nxt;
        }
        vec_idx -= vec_stride;
    }
    if(vec_idx < num_vecs)
    {
#pragma unroll
        for(size_t j = 0; j < vec_size_i; j++)
        {
            absMax = max(absMax, abs(static_cast<float>(vec_cur[j])));
        }
    }
    // double load core loop end

    absMax = block_reduce<float, aiter::Max, BlockSize, true>(absMax, aiter::Max());

    float row_scale = std::is_same_v<DTYPE_O, opus::fp4_t>
                          ? aiter::fp4_f32_to_e8m0_scale(absMax)
                          : absMax * inverted_DTYPE_MAX;
    return std::make_tuple(row_scale, reinterpret_cast<DTYPE_I*>(&vec_cur));
}

__device__ __forceinline__ float atomicMaxFloat(float *addr, float value)
  {
    float old;
    old = (value >= 0)
              ? __int_as_float(atomicMax((int *)addr, __float_as_int(value)))
              : __uint_as_float(
                    atomicMin((unsigned int *)addr, __float_as_uint(value)));

    return old;
  }

template <typename DTYPE_I, typename DTYPE_O>
__global__ void
data_to_scale_kernel(float* __restrict__ scale, const DTYPE_I* __restrict__ input, const int cols)
{
    auto res        = data_to_per_row_scale<DTYPE_I, DTYPE_O, 0>(input, cols);
    float row_scale = std::get<0>(res);
    if(threadIdx.x == 0)
    {
        atomicMaxFloat(scale, row_scale);
    }
}

template <typename DTYPE_I, typename DTYPE_O>
__device__ void scaled_quant_impl(DTYPE_O* __restrict__ out,
                                  const DTYPE_I* __restrict__ input,
                                  const float* __restrict__ scale,
                                  const int32_t cols)
{

    const float inverted_scale =
        std::is_same_v<DTYPE_O, opus::fp4_t> ? (*scale) : __builtin_amdgcn_rcpf(*scale);
    static constexpr int32_t vec_size_i = 16 / sizeof(DTYPE_O);
    static constexpr int32_t vec_size_o =
        std::is_same_v<DTYPE_O, opus::fp4_t> ? vec_size_i / 2 : vec_size_i;

    using vec_i       = opus::vector_t<DTYPE_I, vec_size_i>;
    using DTYPE_STORE = std::conditional_t<std::is_same_v<DTYPE_O, opus::fp4_t>, uint8_t, DTYPE_O>;

    const int64_t row_offset        = blockIdx.x * cols;
    auto const* ptr_i               = reinterpret_cast<DTYPE_I const*>(input + row_offset);
    auto const* input_vecs          = reinterpret_cast<vec_i const*>(ptr_i);
    auto* ptr_o                     = std::is_same_v<DTYPE_O, opus::fp4_t>
                                          ? reinterpret_cast<DTYPE_STORE*>(out + row_offset / 2)
                                          : reinterpret_cast<DTYPE_STORE*>(out + row_offset);
    static constexpr int32_t ooba_i = 4 / sizeof(DTYPE_I);
    static constexpr int32_t ooba_o = 4 / sizeof(DTYPE_O);
    const int32_t oob_i             = (cols + ooba_i - 1) / ooba_i * ooba_i;
    const int32_t oob_o             = (cols + ooba_o - 1) / ooba_o * ooba_o;

    auto buffer_i = opus::make_gmem<DTYPE_I>(ptr_i, oob_i * sizeof(DTYPE_I));
    auto buffer_o = opus::make_gmem<DTYPE_STORE>(ptr_o, oob_o * sizeof(DTYPE_STORE));

    // double load core loop start
    const int32_t num_elems_tail = cols % vec_size_i;
    const int32_t num_vecs       = (cols + vec_size_i - 1) / vec_size_i;
    const int32_t tail_thread    = num_vecs % BlockSize;
    vec_i vec_nxt;
    vec_i vec_cur;
    // size_t vec_idx = threadIdx.x * vec_size_i;
    // size_t vec_stride = BlockSize * vec_size_i;
    size_t vec_idx    = threadIdx.x;
    size_t vec_stride = BlockSize;
    if(vec_idx < num_vecs)
    {
        vec_cur = load_vector_nbytes<DTYPE_I, vec_size_i, 16>(buffer_i, vec_idx * vec_size_i);
    }

    for(vec_idx += vec_stride; vec_idx < num_vecs; vec_idx += vec_stride)
    {
        vec_nxt = load_vector_nbytes<DTYPE_I, vec_size_i, 16>(buffer_i, vec_idx * vec_size_i);
        store_vector<DTYPE_STORE, DTYPE_I, vec_size_i, RT, false, WARP_SIZE, 1, DTYPE_O>(buffer_o, vec_cur, (vec_idx - vec_stride) * vec_size_o, inverted_scale);
        vec_cur = vec_nxt;
    }

    if(vec_idx - vec_stride < num_vecs)
    {
        store_vector<DTYPE_STORE, DTYPE_I, vec_size_i, RT, false, WARP_SIZE, 1, DTYPE_O>(buffer_o, vec_cur, (vec_idx - vec_stride) * vec_size_o, inverted_scale);
    }
    // double load core loop end
}

template <typename DTYPE_I, typename DTYPE_O, int thread_data_size = 16>
__device__ void scaled_quant_vgpr_impl(DTYPE_O* __restrict__ out,
                                       DTYPE_I* __restrict__ input,
                                       const float* __restrict__ scale,
                                       const int cols,
                                       int64_t out_offset)
{

    const float inverted_scale =
        std::is_same_v<DTYPE_O, opus::fp4_t> ? (*scale) : __builtin_amdgcn_rcpf(*scale);
    static constexpr int32_t vec_size_i = thread_data_size;
    static constexpr int32_t vec_size_o =
        std::is_same_v<DTYPE_O, opus::fp4_t> ? vec_size_i / 2 : vec_size_i;

    using vec_i       = opus::vector_t<DTYPE_I, vec_size_i>;
    using DTYPE_STORE = std::conditional_t<std::is_same_v<DTYPE_O, opus::fp4_t>, uint8_t, DTYPE_O>;

    auto const* ptr_i               = reinterpret_cast<DTYPE_I const*>(input);
    auto const* input_vecs          = reinterpret_cast<vec_i const*>(ptr_i);
    auto* out_ptr                   = reinterpret_cast<DTYPE_O*>(out);
    auto* ptr_o                     = std::is_same_v<DTYPE_O, opus::fp4_t>
                                          ? reinterpret_cast<DTYPE_STORE*>(out + out_offset / 2)
                                          : reinterpret_cast<DTYPE_STORE*>(out + out_offset);
    static constexpr int32_t ooba_i = 4 / sizeof(DTYPE_I);
    static constexpr int32_t ooba_o = 4 / sizeof(DTYPE_O);
    const int32_t oob_i             = (cols + ooba_i - 1) / ooba_i * ooba_i;
    const int32_t oob_o             = (cols + ooba_o - 1) / ooba_o * ooba_o;

    auto buffer_o = opus::make_gmem<DTYPE_STORE>(ptr_o, oob_o * sizeof(DTYPE_STORE));
    const int32_t num_vecs = (cols + vec_size_i - 1) / vec_size_i;

    if(threadIdx.x < num_vecs)
    {
        store_vector<DTYPE_STORE, DTYPE_I, thread_data_size, RT, false, WARP_SIZE, 1, DTYPE_O>(buffer_o, *input_vecs, threadIdx.x * vec_size_o, inverted_scale);
    }
}

template <typename DTYPE_I, typename DTYPE_O>
__global__ void scaled_quant_kernel(DTYPE_O* __restrict__ out,
                                    const DTYPE_I* __restrict__ input,
                                    const float* __restrict__ scale,
                                    const int cols)
{
    scaled_quant_impl<DTYPE_I>(out, input, scale, cols);
}

template <typename DTYPE_I, typename DTYPE_O, int thread_data_size = 16>
__global__ void
dynamic_per_token_scaled_quant_kernel(DTYPE_O* __restrict__ out,
                                      float* __restrict__ scale,
                                      DTYPE_I* __restrict__ input,
                                      float const* __restrict__ scale_ub,
                                      const int32_t cols,
                                      int32_t const* __restrict__ num_rows = nullptr,
                                      const int32_t num_rows_factor        = 1)
{
    const int token_idx = blockIdx.x;
    if(num_rows != nullptr)
    {
        int32_t rows = *num_rows * num_rows_factor;
        if(token_idx >= rows)
            return;
    }
    auto res         = data_to_per_row_scale<DTYPE_I, DTYPE_O, thread_data_size>(input, cols);
    float row_scale  = std::get<0>(res);
    DTYPE_I* vec_ptr = std::get<1>(res);

    if(threadIdx.x == 0)
    {
        if constexpr(std::is_same_v<DTYPE_O, opus::fp4_t>)
        {
            auto* tmp        = reinterpret_cast<uint8_t*>(scale);
            uint8_t exponent = (__builtin_bit_cast(uint32_t, row_scale) >> 23) & 0b11111111;
            tmp[token_idx]   = exponent;
        }
        else
        {
            scale[token_idx] = row_scale;
        }
    }

    if constexpr(thread_data_size == 0)
    {
        scaled_quant_impl<DTYPE_I>(out, input, &row_scale, cols);
    }
    else
    {
        const int64_t row_offset = blockIdx.x * cols;
        scaled_quant_vgpr_impl<DTYPE_I, DTYPE_O, thread_data_size>(out, vec_ptr, &row_scale, cols, row_offset);
    }
}

template <typename DTYPE_I, typename DTYPE_O, int block_size, int thread_data_size = 16>
__device__ std::tuple<float, float*>
smooth_data_to_per_row_scale(const DTYPE_I* __restrict__ input,
                             const float* __restrict__ smooth_scale,
                             int32_t smscale_map_idx,
                             const int32_t cols)
{
    static constexpr int32_t vec_size_i =
        thread_data_size == 0 ? 16 / sizeof(DTYPE_O) : thread_data_size;
    static constexpr int32_t vec_size_o =
        std::is_same_v<DTYPE_O, opus::fp4_t> ? vec_size_i / 2 : vec_size_i;
    using vec_s = opus::vector_t<float, vec_size_i>;
    const float inverted_DTYPE_MAX =
        (1. / static_cast<float>(opus::finfo<DTYPE_O>::max()));

    auto const* ptr_smscale = reinterpret_cast<float const*>(smooth_scale + smscale_map_idx * cols);
    auto const* smscale_vecs = reinterpret_cast<vec_s const*>(ptr_smscale);
    auto buffer_s = opus::make_gmem<float>(ptr_smscale, cols * sizeof(float));

    vec_s smscale_cur;
    size_t vec_idx = threadIdx.x;
    float absMax   = 1e-10f;
    smscale_cur = load_vector_nbytes<float, thread_data_size, 16>(buffer_s, vec_idx * vec_size_i);
#pragma unroll
    for(size_t j = 0; j < vec_size_i; j++)
    {
        smscale_cur[j] = static_cast<float>(input[j]) * smscale_cur[j];
        absMax         = max(absMax, abs(smscale_cur[j]));
    }

    absMax = block_reduce<float, aiter::Max, block_size, true>(absMax, aiter::Max());

    float row_scale = std::is_same_v<DTYPE_O, opus::fp4_t>
                          ? aiter::fp4_f32_to_e8m0_scale(absMax)
                          : absMax * inverted_DTYPE_MAX;
    return std::make_tuple(row_scale, reinterpret_cast<float*>(&smscale_cur));
}

template <typename DTYPE_I, typename DTYPE_O, int block_size, int thread_data_size = 16, bool transpose_out_dim01 = false, bool has_smscale_map = false, bool has_smscale_hash = false, int max_smscale_map_hash_size = 1024>
__global__ void smooth_per_token_scaled_quant_kernel(DTYPE_O* __restrict__ out,
                                                     float* __restrict__ scale,
                                                     DTYPE_I* __restrict__ input,
                                                     float* __restrict__ smooth_scale,
                                                     int* __restrict__ smooth_scale_map,
                                                     int* __restrict__ smooth_scale_map_hash,
                                                     const int32_t num_tg,
                                                     const int32_t cols,
                                                     int32_t const* __restrict__ num_rows = nullptr,
                                                     const int32_t num_rows_factor        = 1,
                                                     const int32_t input_dim0             = 1,
                                                     const int32_t input_dim1             = 1,
                                                     const int32_t input_stride0_cols     = 1,
                                                     const int32_t input_stride1_cols     = 1,
                                                     const int32_t out_stride0_cols       = 1,
                                                     const int32_t out_stride1_cols       = 1,
                                                     const int32_t smooth_scale_map_hash_size = 256)
{
    __shared__ int32_t smooth_scale_map_hash_shared[1024];
    // const int num_tg = gridDim.x;
    int rows = num_rows == nullptr ? input_dim0 * input_dim1 : *num_rows * num_rows_factor;
    if constexpr(has_smscale_hash)
    {
        auto buffer_hash = opus::make_gmem<int>(smooth_scale_map_hash, smooth_scale_map_hash_size * sizeof(int));
        constexpr int32_t async_load_num = (max_smscale_map_hash_size + block_size - 1) / block_size;
        static_assert(max_smscale_map_hash_size <= 1024, "max_smscale_map_hash_size must be less than 1024");
        #pragma unroll
        for(int i = 0; i < async_load_num; i++)
        {
#if defined(__GFX9__)
            const int lds_ptr_sgpr = __builtin_amdgcn_readfirstlane((reinterpret_cast<uintptr_t>((smooth_scale_map_hash_shared + threadIdx.x / WARP_SIZE * WARP_SIZE + i * block_size))));
            uint32_t offset = threadIdx.x * sizeof(int) + i * block_size * sizeof(int);
            asm volatile( "s_mov_b32 m0 %0\n\t"
                "buffer_load_dword %1, %2, 0 offen offset:0 lds\n\t"
                ::"s"(lds_ptr_sgpr), "v"(offset), "s"(buffer_hash.cached_rsrc): "memory", "m0");
#else
            buffer_hash.async_load(smooth_scale_map_hash_shared + threadIdx.x + i * block_size, threadIdx.x + i * block_size);
#endif
        }
    }

    const int rows_per_tg = rows / num_tg;
    const int remainder   = rows - rows_per_tg * num_tg;
    const int chunk_start = blockIdx.x < remainder
                          ? blockIdx.x * (rows_per_tg + 1)
                          : remainder * (rows_per_tg + 1) + (blockIdx.x - remainder) * rows_per_tg;
    const int chunk_size  = rows_per_tg + (blockIdx.x < remainder ? 1 : 0);
    const int chunk_end   = chunk_start + chunk_size;
    const int lane_idx    = threadIdx.x % WARP_SIZE;

    int smscale_map_idx_list = 0;
    int pre_real_token_idx = -1;
    for(int i = 0; i < chunk_size; i++)
    {
        int i_rem = i & (WARP_SIZE - 1);
        if constexpr(has_smscale_map)
        {
            if (i_rem == 0)
            {
                auto buffer_map = opus::make_gmem<int>(smooth_scale_map + chunk_start, chunk_size * sizeof(int));
                smscale_map_idx_list = buffer_map.load(lane_idx + i)[0];
#if defined(__gfx1250__)
                opus::s_wait_loadcnt(opus::number<0>{});
#else
                opus::s_waitcnt_vmcnt(opus::number<0>{});
#endif
                if (i == 0)
                {
                    __syncthreads();
                }
                if constexpr(has_smscale_hash)
                {
                    smscale_map_idx_list = smooth_scale_map_hash_shared[smscale_map_idx_list];
                }
            }
            
        }
        int token_idx = chunk_start + i;
        int idx_input_dim0 = token_idx / input_dim1;
        int idx_input_dim1 = token_idx % input_dim1;
        int real_token_idx = idx_input_dim1 * input_stride1_cols +
                            idx_input_dim0 * input_stride0_cols;
        int32_t smscale_map_idx = __builtin_amdgcn_readlane(smscale_map_idx_list, i_rem);
       
        if (smscale_map_idx < 0)
        {
            continue;
        }
        static constexpr int32_t vec_size_i =
            thread_data_size == 0 ? 16 / sizeof(DTYPE_O) : thread_data_size;
        static constexpr int32_t load_chunk_bytes = sizeof(DTYPE_I) * vec_size_i % 16 == 0 ? 16 : (sizeof(DTYPE_I) * vec_size_i % 8 == 0 ? 8 : 4);
        // using vec_i = opus::vector_t<DTYPE_I, vec_size_i>;
        using vec_i = opus::vector_t<DTYPE_I, vec_size_i>;
        using vec_f = opus::vector_t<float, vec_size_i>;

        vec_f vec_input_f;
        float* input_f_ptr = reinterpret_cast<float*>(&vec_input_f);
        if (real_token_idx != pre_real_token_idx)
        {
            pre_real_token_idx = real_token_idx;
            auto buffer_input = opus::make_gmem<DTYPE_I>(input + (int64_t)real_token_idx * (int64_t)cols, cols * sizeof(DTYPE_I));
            vec_i vec_input = load_vector_nbytes<DTYPE_I, vec_size_i, load_chunk_bytes, RT>(buffer_input, threadIdx.x * vec_size_i);
            for(int i = 0; i < vec_size_i; i++)
            {
                vec_input_f[i] = static_cast<float>(vec_input[i]);
            }
        }
        auto res = smooth_data_to_per_row_scale<float, DTYPE_O, block_size, thread_data_size>(
            input_f_ptr, smooth_scale, smscale_map_idx, cols);
        float row_scale = std::get<0>(res);
        float* vec_ptr  = std::get<1>(res);

        int out_token_idx;
        if constexpr(transpose_out_dim01)
        {   
            int idx_out_dim0 = token_idx / input_dim0;
            int idx_out_dim1 = token_idx % input_dim0;
            out_token_idx = idx_out_dim1 * out_stride1_cols +
                            idx_out_dim0 * out_stride0_cols;
        }
        else
        {
            out_token_idx = idx_input_dim1 * out_stride1_cols +
                            idx_input_dim0 * out_stride0_cols;
        }
        if(threadIdx.x == 0)
        {
            if constexpr(std::is_same_v<DTYPE_O, opus::fp4_t>)
            {
                auto* tmp        = reinterpret_cast<uint8_t*>(scale);
                uint8_t exponent = (__builtin_bit_cast(uint32_t, row_scale) >> 23) & 0b11111111;
                tmp[out_token_idx]   = exponent;
            }
            else
            {
                scale[out_token_idx] = row_scale;
            }
        }

        int64_t out_offset = (int64_t)out_token_idx * (int64_t)cols;    
        scaled_quant_vgpr_impl<float, DTYPE_O, thread_data_size>(out, vec_ptr, &row_scale, cols, out_offset);
    }
}

void static_per_tensor_quant(aiter_tensor_t& out,         // [..., d]
                             const aiter_tensor_t& input, // [..., d]
                             const aiter_tensor_t& scale) // [1]
{
    const int cols = input.size(-1);
    int rows       = input.numel() / cols;
    dim3 grid(rows);
    dim3 block(BlockSize);
    HipDeviceGuard device_guard(input.device_id);
    const hipStream_t stream = aiter::getCurrentHIPStream();
    if(out.dtype() == AITER_DTYPE_fp8)
    {
        AITER_DISPATCH_FLOATING16_TYPES_rmTorch(input.dtype(), "scaled_quant_kernel", [&] {
            using input_dtype = typename aiter::hip2opus<scalar_t>::type;
            aiter::scaled_quant_kernel<<<grid, block, 0, stream>>>(
                reinterpret_cast<opus::fp8_t*>(out.data_ptr()),
                reinterpret_cast<input_dtype*>(input.data_ptr()),
                reinterpret_cast<float*>(scale.data_ptr()),
                cols);
        });
    }
    else if(out.dtype() == AITER_DTYPE_i8)
    {
        AITER_DISPATCH_FLOATING16_TYPES_rmTorch(input.dtype(), "scaled_quant_kernel", [&] {
            using input_dtype = typename aiter::hip2opus<scalar_t>::type;
            aiter::scaled_quant_kernel<<<grid, block, 0, stream>>>(
                reinterpret_cast<opus::i8_t*>(out.data_ptr()),
                reinterpret_cast<input_dtype*>(input.data_ptr()),
                reinterpret_cast<float*>(scale.data_ptr()),
                cols);
        });
    }
    else
    {
        AITER_CHECK(false, __func__, " not support output type: ", AiterDtype_to_str(out.dtype()));
    }
}

#define DYNAMIC_PER_TOKEN_SCALED_QUANT_KERNEL_IMPL(quant_kernel, DTYPE_O, THREAD_DATA)      \
    AITER_DISPATCH_FLOATING16_TYPES_rmTorch(input.dtype(), "quant_kernel", [&] {             \
        using input_dtype = typename aiter::hip2opus<scalar_t>::type;                        \
        aiter::quant_kernel<input_dtype, DTYPE_O, THREAD_DATA><<<grid, block, 0, stream>>>( \
            reinterpret_cast<DTYPE_O*>(out.data_ptr()),                                     \
            reinterpret_cast<float*>(scales.data_ptr()),                                    \
            reinterpret_cast<input_dtype*>(input.data_ptr()),                               \
            scale_ub.has_value() ? reinterpret_cast<float*>(scale_ub->data_ptr()) : nullptr,            \
            cols,                                                                           \
            num_rows_ptr,                                                                   \
            num_rows_factor);                                                               \
    });

#define DYNAMIC_PER_TOKEN_SCALED_QUANT_KERNEL_DISPATCH(quant_kernel, DTYPE_O, cols) \
    if(cols <= 8 * BlockSize)                                                       \
    {                                                                               \
        DYNAMIC_PER_TOKEN_SCALED_QUANT_KERNEL_IMPL(quant_kernel, DTYPE_O, 8)        \
    }                                                                               \
    else if(cols <= 16 * BlockSize)                                                 \
    {                                                                               \
        DYNAMIC_PER_TOKEN_SCALED_QUANT_KERNEL_IMPL(quant_kernel, DTYPE_O, 16)       \
    }                                                                               \
    else if(cols <= 32 * BlockSize)                                                 \
    {                                                                               \
        DYNAMIC_PER_TOKEN_SCALED_QUANT_KERNEL_IMPL(quant_kernel, DTYPE_O, 32)       \
    }                                                                               \
    else                                                                            \
    {                                                                               \
        DYNAMIC_PER_TOKEN_SCALED_QUANT_KERNEL_IMPL(quant_kernel, DTYPE_O, 0)        \
    }

#define DISPATCH_GROUP_SIZE(gs, ...) \
    if((gs) == 32)        { constexpr int32_t _GS = 32;  __VA_ARGS__ } \
    else if((gs) == 64)   { constexpr int32_t _GS = 64;  __VA_ARGS__ } \
    else                  { constexpr int32_t _GS = 128; __VA_ARGS__ }

void dynamic_per_tensor_quant(aiter_tensor_t& out,         // [..., d]
                              const aiter_tensor_t& input,  // [..., d]
                              aiter_tensor_t& scale)        // [1]
{
    const int cols = input.size(-1);
    int rows       = input.numel() / cols;
    dim3 grid(rows);
    dim3 block(BlockSize);
    HipDeviceGuard device_guard(input.device_id);
    const hipStream_t stream = aiter::getCurrentHIPStream();
    if(out.dtype() == AITER_DTYPE_fp8)
    {
        AITER_DISPATCH_FLOATING16_TYPES_rmTorch(input.dtype(), "scaled_quant_kernel", [&] {
            using input_dtype = typename aiter::hip2opus<scalar_t>::type;
            aiter::initializeScale<<<dim3(1), dim3(64), 0, stream>>>(
                reinterpret_cast<float*>(scale.data_ptr()), 1, 0.0f);
            aiter::data_to_scale_kernel<input_dtype, opus::fp8_t><<<grid, block, 0, stream>>>(
                reinterpret_cast<float*>(scale.data_ptr()), reinterpret_cast<input_dtype*>(input.data_ptr()), cols);
            aiter::scaled_quant_kernel<<<grid, block, 0, stream>>>(
                reinterpret_cast<opus::fp8_t*>(out.data_ptr()),
                reinterpret_cast<input_dtype*>(input.data_ptr()),
                reinterpret_cast<float*>(scale.data_ptr()),
                cols);
        });
    }
    else if(out.dtype() == AITER_DTYPE_i8)
    {
        AITER_DISPATCH_FLOATING16_TYPES_rmTorch(input.dtype(), "scaled_quant_kernel", [&] {
            using input_dtype = typename aiter::hip2opus<scalar_t>::type;
            aiter::initializeScale<<<dim3(1), dim3(64), 0, stream>>>(
                reinterpret_cast<float*>(scale.data_ptr()), 1, 0.0f);
            aiter::data_to_scale_kernel<input_dtype, opus::i8_t><<<grid, block, 0, stream>>>(
                reinterpret_cast<float*>(scale.data_ptr()), reinterpret_cast<input_dtype*>(input.data_ptr()), cols);
            aiter::scaled_quant_kernel<<<grid, block, 0, stream>>>(
                reinterpret_cast<opus::i8_t*>(out.data_ptr()),
                reinterpret_cast<input_dtype*>(input.data_ptr()),
                reinterpret_cast<float*>(scale.data_ptr()),
                cols);
        });
    }
    else
    {
        AITER_CHECK(false, __func__, " not support output type: ", AiterDtype_to_str(out.dtype()));
    }
}

void dynamic_per_token_scaled_quant(aiter_tensor_t& out,         // [..., d]
                                    const aiter_tensor_t& input, // [..., d]
                                    aiter_tensor_t& scales,
                                    std::optional<aiter_tensor_t> scale_ub,
                                    bool shuffle_scale,
                                    std::optional<aiter_tensor_t> num_rows,
                                    int num_rows_factor)
{
    AITER_CHECK(input.is_contiguous());
    AITER_CHECK(out.is_contiguous());

    int const cols        = input.size(-1);
    int const rows        = input.numel() / cols;
    int32_t* num_rows_ptr = num_rows.has_value() ? reinterpret_cast<int32_t*>(num_rows->data_ptr()) : nullptr;

    HipDeviceGuard device_guard(input.device_id);
    const hipStream_t stream = aiter::getCurrentHIPStream();

    if(cols == 32 || cols == 64 || cols == 128)
    {
        DISPATCH_GROUP_SIZE(cols,
            static constexpr int thread_data_size     = 32;
            static constexpr int num_thread_per_group = _GS / thread_data_size;
            auto launch_group_quant = [&](auto out_type_tag, int ori_cols, int ori_rows, int num_group, auto shuffle_tag) {
                using out_t = decltype(out_type_tag);
                constexpr bool ss = decltype(shuffle_tag)::value;
            // Block size follows the group ordering. Row-major reads a longer contiguous
            // run when widened (28.7 -> 26.4 us at 64 -> 256); column-major spans more
            // rows instead, and the lost locality outweighs it (27.0 -> 27.7).
                constexpr bool kColMajor =
                    std::is_same_v<out_t, opus::fp4_t> && ss && _GS == 128;
                // See the note at the other launch site: the 64/256 split is gfx1250-only.
                // See the note at the other launch site: the wide block needs blocks to spare.
                static constexpr int32_t kBlkTuned = kColMajor ? 64 : 256;
                const int simds_bs = static_cast<int>(get_num_cu_func()) * 4;
                const bool wide_ok =
                    dyn_gq_tuned_arch() &&
                    (static_cast<int64_t>(num_group) * num_thread_per_group / kBlkTuned) >=
                        static_cast<int64_t>(simds_bs);   // one block per SIMD, unswept path
                const int32_t blk_rt = wide_ok ? kBlkTuned : 64;
                const int num_group_per_tg = blk_rt / num_thread_per_group;
                static constexpr int32_t ooba = 4 / sizeof(out_t);
                const int64_t oob_elems =
                    (static_cast<int64_t>(ori_rows) * ori_cols + ooba - 1) / ooba * ooba;
                const int64_t oob_size = oob_elems * static_cast<int64_t>(sizeof(out_t));
                dim3 const grid((num_group + num_group_per_tg - 1) / num_group_per_tg);
                dim3 const block(blk_rt);
                AITER_DISPATCH_FLOATING16_TYPES_rmTorch(
                    input.dtype(), "dynamic_per_group_scaled_quant_kernel", [&] {
                        using input_dtype = typename aiter::hip2opus<scalar_t>::type;
                        auto launch_bs = [&](auto blk_tag) {
                        constexpr int32_t BS = decltype(blk_tag)::value;
                        aiter::dynamic_per_group_scaled_quant_kernel<input_dtype, out_t, thread_data_size, _GS, ss, BS>
                            <<<grid, block, 0, stream>>>(
                            reinterpret_cast<out_t*>(out.data_ptr()),
                            reinterpret_cast<float*>(scales.data_ptr()),
                            reinterpret_cast<input_dtype*>(input.data_ptr()),
                            scale_ub.has_value() ? reinterpret_cast<float*>(scale_ub->data_ptr()) : nullptr,
                            ori_rows,
                            ori_cols,
                            ori_cols,
                            oob_size,
                            num_rows_ptr,
                            num_rows_factor);
                        };
                        if(blk_rt == kBlkTuned)
                            launch_bs(std::integral_constant<int32_t, kBlkTuned>{});
                        else
                            launch_bs(std::integral_constant<int32_t, 64>{});
                    });
            };
            auto do_launch = [&](auto shuffle_tag) {
                if(out.dtype() == AITER_DTYPE_fp8)
                {
                    int ori_cols  = out.size(-1);
                    int ori_rows  = rows / (ori_cols / _GS);
                    launch_group_quant(opus::fp8_t{}, ori_cols, ori_rows, rows, shuffle_tag);
                }
                else if(out.dtype() == AITER_DTYPE_i8)
                {
                    int ori_cols  = _GS;
                    int ori_rows  = rows;
                    launch_group_quant(opus::i8_t{}, ori_cols, ori_rows, rows, shuffle_tag);
                }
#if defined(__Float4_e2m1fn_x2)
                else if(out.dtype() == AITER_DTYPE_fp4x2)
                {
                    int ori_cols  = out.size(-1) * 2;
                    int ori_rows  = rows / (ori_cols / _GS);
                    constexpr bool ss = decltype(shuffle_tag)::value;
                    int num_group = ss ? ori_rows * (((ori_cols / _GS) + 7) / 8 * 8) : rows;
                    launch_group_quant(opus::fp4_t{}, ori_cols, ori_rows, num_group, shuffle_tag);
                }
#endif
                else
                {
                    AITER_CHECK(false, __func__, " not support output type: ", AiterDtype_to_str(out.dtype()));
                }
            };
            if(shuffle_scale)
                do_launch(std::true_type{});
            else
                do_launch(std::false_type{});
        )
    }
    else
    {
        dim3 const grid(rows);
        dim3 const block(BlockSize);
        if(out.dtype() == AITER_DTYPE_fp8)
        {
            DYNAMIC_PER_TOKEN_SCALED_QUANT_KERNEL_DISPATCH(
                dynamic_per_token_scaled_quant_kernel, opus::fp8_t, cols);
        }
        else if(out.dtype() == AITER_DTYPE_i8)
        {
            DYNAMIC_PER_TOKEN_SCALED_QUANT_KERNEL_DISPATCH(
                dynamic_per_token_scaled_quant_kernel, opus::i8_t, cols);
        }
#if defined(__Float4_e2m1fn_x2)
        else if(out.dtype() == AITER_DTYPE_fp4x2)
        {
            DYNAMIC_PER_TOKEN_SCALED_QUANT_KERNEL_DISPATCH(
                dynamic_per_token_scaled_quant_kernel, opus::fp4_t, cols);
        }
#endif
        else
        {
            AITER_CHECK(false, __func__, " not support output type: ", AiterDtype_to_str(out.dtype()));
        }
    }
}

// MXFP8 1x32 quant whose e8m0 scale is written straight into the gfx1250 MXFP8 ASM GEMM's
// A-scale layout ("m32k4", the byte layout of shuffle_mxfp8fp4_scale):
//     scale[(m / 32) * K + (g / 4) * 128 + (m % 32) * 4 + g % 4],   g = k / 32
// i.e. one contiguous 128B block per 32 rows x 128 columns. Rows are padded to a multiple of
// 32; the pad rows' scales are written as 0x7F (2^0, the shuffle_mxfp8fp4_scale pad value),
// so the caller only allocates (pad32(M), K/32) bytes and never pre-fills it.
//
// A lane holds 16 contiguous elements (32B load, 16B fp8 store) and two lanes form a group, so
// a 32-lane slice covers four rows of a 32x128 tile per step. A slice runs kSteps steps and
// 32 / (4 * kSteps) slices share a tile; all of a slice's loads are issued before its first
// convert. The only cross-lane op is a quad-perm DPP, so nothing depends on the wave size. The
// amax / e8m0 rounding / saturation below mirror the g32 path of
// dynamic_per_group_scaled_quant_kernel, so the fp8 output and the unshuffled scale are
// bit-identical to that path.
template <typename DTYPE_I, int kWaves, int kSteps>
__global__ void __launch_bounds__(kWaves * 32)
dynamic_per_group_quant_m32k4_kernel(opus::fp8_t* __restrict__ out,
                                     uint8_t* __restrict__ scale,
                                     DTYPE_I const* __restrict__ input,
                                     int64_t rows,
                                     int32_t cols,
                                     int64_t row_stride)
{
    static constexpr int kElems = 16; // per lane: two lanes per 32-element group
    // A slice covers 4 * kSteps rows of one 128-column tile; kSlices slices cover its 32 rows.
    static constexpr int kSlices = 32 / (4 * kSteps);
    static_assert(kSteps * 4 * kSlices == 32 && kWaves % kSlices == 0);
    using vec_i = opus::vector_t<DTYPE_I, kElems>;
    using vec_o = opus::vector_t<opus::fp8_t, kElems>;
#if defined(__gfx1250__)
    static constexpr bool kHwConvertDiv = std::is_same_v<DTYPE_I, opus::bf16_t>;
#else
    static constexpr bool kHwConvertDiv = false;
#endif
    static constexpr bool kScaleMayClip =
        aiter::kDefaultMxScaleRoundMode == aiter::MxScaleRoundMode::RoundDown ||
        aiter::kDefaultMxScaleRoundMode == aiter::MxScaleRoundMode::Even;
#if defined(__gfx942__)
    static constexpr aiter::MxDtype kMxDtype = aiter::MxDtype::FP8_E4M3_FNUZ;
#else
    static constexpr aiter::MxDtype kMxDtype = aiter::MxDtype::FP8_E4M3;
#endif

    const int lane     = threadIdx.x % 32;
    const int slice    = threadIdx.x / 32;
    const int32_t kt   = blockIdx.x * (kWaves / kSlices) + slice / kSlices; // 128-column tile
    if(kt * 128 >= cols)
        return;
    const int64_t row0 = static_cast<int64_t>(blockIdx.y) * 32;
    const int rbase    = (slice % kSlices) * 4 * kSteps; // first row of this slice in the tile
    const int rsub     = rbase + lane / 8;
    const int32_t col  = kt * 128 + (lane % 8) * kElems;

    vec_i data[kSteps];
    opus::static_for<kSteps>([&](auto s) {
        const int64_t row = row0 + 4 * s.value + rsub;
        if(row < rows)
            data[s.value] = *reinterpret_cast<vec_i const*>(input + row * row_stride + col);
    });

    uint8_t* scale_blk = scale + (row0 / 32) * cols + kt * 128;
    opus::static_for<kSteps>([&](auto s) {
        const int r        = 4 * s.value + rsub; // row within the 32-row block
        const bool valid   = row0 + r < rows;
        vec_i thread_data  = data[s.value];
        float absMax       = 1e-10f;
        for(int j = 0; j < kElems; ++j)
            absMax = max(absMax, abs(static_cast<float>(thread_data[j])));
        absMax = multithread_reduce(absMax, aiter::Max(), 2);
        bool degenerate_group = false;
        if constexpr(kHwConvertDiv)
        {
            degenerate_group = !(absMax < __builtin_inff());
            absMax           = fminf(absMax, 448.0f * 0x1.0p119f);
        }
        const float row_scale =
            aiter::fp_f32_to_e8m0_scale<aiter::kDefaultMxScaleRoundMode, kMxDtype>(absMax);
        if(lane % 2 == 0)
            scale_blk[r * 4 + (lane % 8) / 2] =
                valid ? static_cast<uint8_t>((__builtin_bit_cast(uint32_t, row_scale) >> 23) & 0xFF)
                      : uint8_t{0x7F};
        if(!valid)
            return;
        if constexpr(kHwConvertDiv)
        {
            if(kScaleMayClip || degenerate_group)
            {
                const float hi = 448.0f * row_scale;
                for(int j = 0; j < kElems; ++j)
                {
                    const float v = static_cast<float>(thread_data[j]);
                    if(v > hi)
                        thread_data[j] = static_cast<DTYPE_I>(hi);
                    if(v < -hi)
                        thread_data[j] = static_cast<DTYPE_I>(-hi);
                }
            }
        }
        const vec_o q = scaled_cast_div<opus::fp8_t>(thread_data, row_scale);
        *reinterpret_cast<vec_o*>(out + (row0 + r) * cols + col) = q;
    });
}

static void dynamic_per_group_quant_m32k4(aiter_tensor_t& out,
                                          const aiter_tensor_t& input,
                                          aiter_tensor_t& scales,
                                          int group_size,
                                          const std::optional<aiter_tensor_t>& num_rows,
                                          hipStream_t stream)
{
    int const cols       = input.size(-1);
    int64_t const rows   = input.numel() / cols;
    int64_t const rstride = input.ndim > 1 ? input.stride(-2) : cols;
    AITER_CHECK(group_size == 32, __func__, " m32k4 scale layout needs group_size 32, got ", group_size);
    AITER_CHECK(out.dtype() == AITER_DTYPE_fp8, __func__, " m32k4 scale layout needs fp8 output, got ",
                AiterDtype_to_str(out.dtype()));
    AITER_CHECK(scales.dtype() == AITER_DTYPE_fp8_e8m0 || scales.dtype() == AITER_DTYPE_u8, __func__,
                " m32k4 scale layout needs an e8m0/u8 scale, got ", AiterDtype_to_str(scales.dtype()));
    AITER_CHECK(!num_rows.has_value(), __func__, " m32k4 scale layout does not support num_rows");
    AITER_CHECK(input.dtype() == AITER_DTYPE_bf16 || input.dtype() == AITER_DTYPE_fp16, __func__,
                " m32k4 scale layout needs bf16/fp16 input, got ", AiterDtype_to_str(input.dtype()));
    AITER_CHECK(cols % 128 == 0, __func__, " m32k4 scale layout needs K % 128 == 0, got ", cols);
    AITER_CHECK(input.stride(-1) == 1 && rstride % 16 == 0 &&
                    reinterpret_cast<uintptr_t>(input.data_ptr()) % 32 == 0,
                __func__, " m32k4 quant needs 32B-aligned contiguous input rows");
    AITER_CHECK(out.is_contiguous() && scales.is_contiguous(), __func__,
                " m32k4 quant needs contiguous out and scales");
    const int64_t rows_pad = (rows + 31) / 32 * 32;
    AITER_CHECK(scales.numel() >= rows_pad * (cols / 32), __func__,
                " m32k4 scale needs (pad32(M), K/32) = (", rows_pad, ", ", cols / 32,
                ") bytes, got numel ", scales.numel());
    AITER_CHECK(rows_pad / 32 <= 65535, __func__, " m32k4 quant supports M <= 2097120, got ", rows);
    if(rows == 0)
        return;

    const int ktiles = cols / 128;
    auto launch = [&](auto waves_tag, auto steps_tag) {
        constexpr int kWaves  = decltype(waves_tag)::value;
        constexpr int kSteps  = decltype(steps_tag)::value;
        constexpr int kTilesPerBlock = kWaves / (32 / (4 * kSteps));
        dim3 const grid((ktiles + kTilesPerBlock - 1) / kTilesPerBlock,
                        static_cast<uint32_t>(rows_pad / 32));
        dim3 const block(kWaves * 32);
        AITER_DISPATCH_FLOATING16_TYPES_rmTorch(input.dtype(), "dynamic_per_group_quant_m32k4", [&] {
            using input_dtype = typename aiter::hip2opus<scalar_t>::type;
            dynamic_per_group_quant_m32k4_kernel<input_dtype, kWaves, kSteps>
                <<<grid, block, 0, stream>>>(reinterpret_cast<opus::fp8_t*>(out.data_ptr()),
                                             reinterpret_cast<uint8_t*>(scales.data_ptr()),
                                             reinterpret_cast<input_dtype const*>(input.data_ptr()),
                                             rows,
                                             cols,
                                             rstride);
        });
    };
    // One 4-row step per wave, eight waves per 32x128 tile.
    // Fewer rows per wave means more waves in flight; 8x1 gives 8 waves/SIMD at this shape.
    launch(std::integral_constant<int, 8>{}, std::integral_constant<int, 1>{});
}

// Canonical dynamic per-group scaled quant. Accepts fp8 / i8 / fp4x2 output;
// the per-group scale layout is selected by ``scales.dtype()``:
//   * AITER_DTYPE_fp8_e8m0 / u8 -> e8m0 byte scale (one byte per group of
//     ``group_size`` elements). Required for MXFP4/MXFP8 GEMM consumers.
//   * AITER_DTYPE_fp32          -> continuous fp32 per-group scale.
// fp4 outputs always emit e8m0 (there is no fp32-scale fp4 path); fp8 picks
// the path by scale dtype; i8 only supports fp32. The legacy entry point
// `dynamic_per_group_scaled_quant_fp4` (kept as a forwarder below) hard-coded
// fp4 only, which made the MXFP8 1xG byte-scale path unreachable here.
void dynamic_per_group_scaled_quant(aiter_tensor_t& out,         // [..., d]
                                    const aiter_tensor_t& input, // [..., d]
                                    aiter_tensor_t& scales,
                                    int group_size,
                                    bool shuffle_scale,
                                    std::optional<aiter_tensor_t> num_rows,
                                    int num_rows_factor,
                                    bool scale_layout_m32k4)
{
    if(scale_layout_m32k4)
    {
        // Separate kernel; shuffle_scale / num_rows_factor do not apply to this layout.
        HipDeviceGuard device_guard(input.device_id);
        dynamic_per_group_quant_m32k4(
            out, input, scales, group_size, num_rows, aiter::getCurrentHIPStream());
        return;
    }
    AITER_CHECK(group_size == 32 || group_size == 64 || group_size == 128,
                __func__,
                " only support group_size [32, 64 , 128]");
    AITER_CHECK(out.is_contiguous());

    int const cols        = input.size(-1);
    int const rows        = input.numel() / cols;
    int const row_stride  = input.stride(-2);
    int32_t* num_rows_ptr = num_rows.has_value() ? reinterpret_cast<int32_t*>(num_rows->data_ptr()) : nullptr;

    AITER_CHECK(cols % group_size == 0, __func__, " cols is not divisible by group_size");

    // Decide e8m0 vs fp32 scale path from scales.dtype(). Note u8 alias is
    // accepted for callers that build the scale tensor as raw uint8.
    const bool use_e8m0_scale =
        scales.dtype() == AITER_DTYPE_fp8_e8m0 || scales.dtype() == AITER_DTYPE_u8;
    AITER_CHECK(use_e8m0_scale || scales.dtype() == AITER_DTYPE_fp32,
                __func__,
                " expects scales.dtype in {fp8_e8m0, u8, fp32}, got ",
                AiterDtype_to_str(scales.dtype()));

    HipDeviceGuard device_guard(input.device_id);
    const hipStream_t stream = aiter::getCurrentHIPStream();

    DISPATCH_GROUP_SIZE(group_size,
        static constexpr int thread_data_size     = 32;
        static constexpr int num_thread_per_group = _GS / thread_data_size;
        int scaleN    = cols / _GS;

        auto launch = [&](auto out_type_tag, auto shuffle_tag, auto e8m0_tag) {
            using out_t = decltype(out_type_tag);
            constexpr bool ss = decltype(shuffle_tag)::value;
            constexpr bool ee = decltype(e8m0_tag)::value;
            // Column-major group order: consecutive groups walk x at a fixed y, which is
            // what makes the transposed scale store contiguous. It also gates TDM staging,
            // whose tile only forms under this order.
            constexpr bool kColMajor =
                (std::is_same_v<out_t, opus::fp4_t> || ee) && ss && _GS == 128;
            // Block size is a template argument, so both widths are instantiated and the
            // arch picks at launch; an untuned arch keeps the 64 it always had. TDM decides
            // first, because a staged block scales its tile with the block width (16 KiB
            // per ring slot at 256 against 4 at 64) and the two together measured 22%
            // slower at T=8192. Where staging does not apply, the wide block is worth
            // 4-6% from T=512 to T=4096.
            static constexpr int32_t kBlkTuned = 256;
            const int simds_bs = static_cast<int>(get_num_cu_func()) * 4;
            const int tdm_groups_at_64 = (64 / num_thread_per_group) * kDynGqTdmKPT;
            const bool tdm_wanted =
                dyn_gq_tuned_arch() && kColMajor && kDynGqTdmKPT > 1 &&
                (rows % tdm_groups_at_64) == 0 &&
                (static_cast<int64_t>(rows) * scaleN / tdm_groups_at_64) >=
                    static_cast<int64_t>(simds_bs) * kDynGqTdmMinBlkPerSimd;
            const bool wide_ok =
                dyn_gq_tuned_arch() && !tdm_wanted &&
                (static_cast<int64_t>(rows) * scaleN * num_thread_per_group / kBlkTuned) *
                        kDynGqWideBlkPerSimdDenom >=
                    static_cast<int64_t>(simds_bs);
            const int32_t blk_rt = wide_ok ? kBlkTuned : 64;
            const int num_group_per_tg = blk_rt / num_thread_per_group;
            dim3 const block(blk_rt);
            // e8m0 + shuffle pads scaleN up to a multiple of 8 (tile width)
            // for the MX hw layout (_GS == 32 only); group_size == 128
            // shuffle is a plain transpose, no padding.
            int num_group;
            if constexpr(ee)
            {
                num_group = (ss && _GS == 32) ? rows * ((scaleN + 7) / 8 * 8) : rows * scaleN;
            }
            else
            {
                num_group = rows * scaleN;
            }
            static constexpr int32_t ooba = 4 / sizeof(out_t);
            const int64_t oob_elems =
                (static_cast<int64_t>(rows) * cols + ooba - 1) / ooba * ooba;
            const int64_t oob_size = oob_elems * static_cast<int64_t>(sizeof(out_t));

            // Staging was decided at the block size above, because the two interact; it is
            // named again here only because it also changes the grid.
            const bool use_tdm = tdm_wanted;

            // The grid is deliberately sized by the UNSTAGED block span, so the back half
            // dispatches blocks that reject on the first resolve and retire. Sizing it
            // exactly is the obvious fix and measured 15-24% SLOWER (M=4096/8192/16384),
            // with device code identical and only the launch dimension differing. The
            // mechanism is not understood; this keeps what measures faster.
            dim3 const grid((num_group + num_group_per_tg - 1) / num_group_per_tg);
            AITER_DISPATCH_FLOATING16_TYPES_rmTorch(
                input.dtype(), "dynamic_per_group_scaled_quant_kernel", [&] {
                    using input_dtype = typename aiter::hip2opus<scalar_t>::type;
                    // Opt in only on the measured d01 configuration. Other gfx1250
                    // machines already run this workload faster with the default path.
                    if constexpr(std::is_same_v<input_dtype, opus::bf16_t> &&
                                 std::is_same_v<out_t, opus::fp8_t> && _GS == 128 && ss && ee)
                    {
                        static const bool d01_tuning = [] {
                            const char* value = std::getenv("AITER_DPGSQ_D01_TUNING");
                            return value && value[0] == '1' && value[1] == '\0';
                        }();
                        if(d01_tuning && dyn_gq_tuned_arch() && num_rows_ptr == nullptr &&
                           (rows == 16384 || (rows == 512 && cols == 16384)) && row_stride == cols &&
                           (cols == 7168 || cols == 16384))
                        {
                            if(cols == 7168)
                                aiter::dynamic_per_group_scaled_quant_kernel<
                                    input_dtype, out_t, 32, 128, true, 512, true, false, 0, true, true, false, true, true>
                                    <<<dim3(rows / 256, cols / 128), dim3(512), 0, stream>>>(
                                    reinterpret_cast<out_t*>(out.data_ptr()),
                                    reinterpret_cast<float*>(scales.data_ptr()),
                                    reinterpret_cast<input_dtype*>(input.data_ptr()), nullptr,
                                    rows, cols, row_stride, oob_size, nullptr, num_rows_factor);
                            else
                                aiter::dynamic_per_group_scaled_quant_kernel<
                                    input_dtype, out_t, 32, 128, true, 128, true, true, 16, true, true, true, true>
                                    <<<dim3(cols / 512, (rows + 15) / 16), dim3(128), 0, stream>>>(
                                    reinterpret_cast<out_t*>(out.data_ptr()),
                                    reinterpret_cast<float*>(scales.data_ptr()),
                                    reinterpret_cast<input_dtype*>(input.data_ptr()), nullptr,
                                    rows, cols, row_stride, oob_size, nullptr, num_rows_factor);
                            return;
                        }
                    }
                    auto launch_one = [&](auto tdm_tag, auto blk_tag) {
                    constexpr bool tt = decltype(tdm_tag)::value;
                    constexpr int32_t BS = decltype(blk_tag)::value;
                    aiter::dynamic_per_group_scaled_quant_kernel<input_dtype, out_t, thread_data_size, _GS, ss, BS, ee, tt>
                        <<<grid, block, 0, stream>>>(
                        reinterpret_cast<out_t*>(out.data_ptr()),
                        reinterpret_cast<float*>(scales.data_ptr()),
                        reinterpret_cast<input_dtype*>(input.data_ptr()),
                        nullptr,
                        rows,
                        cols,
                        row_stride,
                        oob_size,
                        num_rows_ptr,
                        num_rows_factor);
                    };
                    using BlkTuned = std::integral_constant<int32_t, kBlkTuned>;
                    using Blk64    = std::integral_constant<int32_t, 64>;
                    if(blk_rt == kBlkTuned)
                    {
                        if(use_tdm) launch_one(std::true_type{},  BlkTuned{});
                        else        launch_one(std::false_type{}, BlkTuned{});
                    }
                    else
                    {
                        if(use_tdm) launch_one(std::true_type{},  Blk64{});
                        else        launch_one(std::false_type{}, Blk64{});
                    }
                });
        };

        auto do_launch = [&](auto shuffle_tag, auto e8m0_tag) {
            constexpr bool ee = decltype(e8m0_tag)::value;
            if(out.dtype() == AITER_DTYPE_fp8)
            {
                launch(opus::fp8_t{}, shuffle_tag, e8m0_tag);
            }
            else if(out.dtype() == AITER_DTYPE_i8)
            {
                static_assert(true, "i8 path does not support e8m0 scale");
                AITER_CHECK(!ee, __func__, " i8 output does not support e8m0 scale");
                launch(opus::i8_t{}, shuffle_tag, std::false_type{});
            }
#if defined(__Float4_e2m1fn_x2)
            else if(out.dtype() == AITER_DTYPE_fp4x2 || out.dtype() == AITER_DTYPE_u8)
            {
                // fp4 always uses e8m0 scale regardless of `use_e8m0_scale`.
                launch(opus::fp4_t{}, shuffle_tag, std::true_type{});
            }
#endif
            else
            {
                AITER_CHECK(false, __func__, " not support output type: ", AiterDtype_to_str(out.dtype()));
            }
        };

        auto with_e8m0 = [&](auto shuffle_tag) {
            if(use_e8m0_scale)
                do_launch(shuffle_tag, std::true_type{});
            else
                do_launch(shuffle_tag, std::false_type{});
        };
        if(shuffle_scale)
            with_e8m0(std::true_type{});
        else
            with_e8m0(std::false_type{});
    )
}

// Backward-compat thin forwarder. Asserts fp4x2/u8 output and delegates to
// the dtype-aware canonical entry. Existing callers (Python compile_ops
// binding `dynamic_per_group_scaled_quant_fp4`, downstream tests, etc.)
// continue to work unchanged.
void dynamic_per_group_scaled_quant_fp4(aiter_tensor_t& out,         // [..., d]
                                        const aiter_tensor_t& input, // [..., d]
                                        aiter_tensor_t& scales,
                                        int group_size,
                                        bool shuffle_scale,
                                        std::optional<aiter_tensor_t> num_rows,
                                        int num_rows_factor)
{
    AITER_CHECK(out.dtype() == AITER_DTYPE_fp4x2 || out.dtype() == AITER_DTYPE_u8,
                __func__,
                " expects fp4x2 / uint8 output; use dynamic_per_group_scaled_quant for fp8/i8");
    dynamic_per_group_scaled_quant(
        out, input, scales, group_size, shuffle_scale, num_rows, num_rows_factor);
}

#define SMOOTH_PER_TOKEN_SCALED_QUANT_KERNEL_IMPL(quant_kernel, DTYPE_O, THREAD_DATA, BLOCK_SIZE, TRANSPOSE_OUT_DIM01, HAS_MAP, HAS_HASH) \
    AITER_DISPATCH_FLOATING16_TYPES_rmTorch(input.dtype(), "quant_kernel", [&] {                                       \
        using input_dtype = typename aiter::hip2opus<scalar_t>::type;                                                  \
        const int cu_num = get_num_cu_func();                                                                          \
        const int max_warp_per_simd = 8;                                                                               \
        const int warp_per_simd = BLOCK_SIZE / (opus::get_warp_size() * 4);                                            \
        int grid_size = enable_ps ? max_warp_per_simd / warp_per_simd * cu_num : rows;                                 \
        dim3 const grid(grid_size);                                                                                    \
        aiter::quant_kernel<input_dtype, DTYPE_O, BLOCK_SIZE, THREAD_DATA, TRANSPOSE_OUT_DIM01, HAS_MAP, HAS_HASH, MAX_EXPERT_SIZE> \
            <<<grid, dim3(BLOCK_SIZE), 0, stream>>>(                                                                   \
                reinterpret_cast<DTYPE_O*>(out.data_ptr()),                                                            \
                reinterpret_cast<float*>(scales.data_ptr()),                                                           \
                reinterpret_cast<input_dtype*>(input.data_ptr()),                                                      \
                reinterpret_cast<float*>(smooth_scale.data_ptr()),                                                     \
                smooth_scale_map_ptr,                                                                                  \
                smooth_scale_map_hash_ptr,                                                                             \
                grid_size,                                                                                             \
                cols,                                                                                                  \
                num_rows_ptr,                                                                                          \
                num_rows_factor,                                                                                       \
                input_dim0,                                                                                            \
                input_dim1,                                                                                            \
                input_stride0_cols,                                                                                    \
                input_stride1_cols,                                                                                    \
                out_stride0_cols,                                                                                      \
                out_stride1_cols,                                                                                      \
                smooth_scale_map_hash_size);                                                                           \
    });

#define SMOOTH_PER_TOKEN_SCALED_QUANT_KERNEL_IMPL_(quant_kernel, DTYPE_O, THREAD_DATA, BLOCK_SIZE)                             \
    if(transpose_out_dim01)                                                                                                    \
    {                                                                                                                          \
        if(smooth_scale_map_ptr != nullptr && smooth_scale_map_hash_ptr != nullptr)                                            \
            SMOOTH_PER_TOKEN_SCALED_QUANT_KERNEL_IMPL(quant_kernel, DTYPE_O, THREAD_DATA, BLOCK_SIZE, true, true, true)        \
        else if(smooth_scale_map_ptr != nullptr)                                                                               \
            SMOOTH_PER_TOKEN_SCALED_QUANT_KERNEL_IMPL(quant_kernel, DTYPE_O, THREAD_DATA, BLOCK_SIZE, true, true, false)       \
        else                                                                                                                   \
            SMOOTH_PER_TOKEN_SCALED_QUANT_KERNEL_IMPL(quant_kernel, DTYPE_O, THREAD_DATA, BLOCK_SIZE, true, false, false)      \
    }                                                                                                                          \
    else                                                                                                                       \
    {                                                                                                                          \
        if(smooth_scale_map_ptr != nullptr && smooth_scale_map_hash_ptr != nullptr)                                            \
            SMOOTH_PER_TOKEN_SCALED_QUANT_KERNEL_IMPL(quant_kernel, DTYPE_O, THREAD_DATA, BLOCK_SIZE, false, true, true)       \
        else if(smooth_scale_map_ptr != nullptr)                                                                               \
            SMOOTH_PER_TOKEN_SCALED_QUANT_KERNEL_IMPL(quant_kernel, DTYPE_O, THREAD_DATA, BLOCK_SIZE, false, true, false)      \
        else                                                                                                                   \
            SMOOTH_PER_TOKEN_SCALED_QUANT_KERNEL_IMPL(quant_kernel, DTYPE_O, THREAD_DATA, BLOCK_SIZE, false, false, false)     \
    }

#define SMOOTH_PER_TOKEN_SCALED_QUANT_KERNEL_DISPATCH(quant_kernel, DTYPE_O, cols)           \
    if(cols <= 8 * BlockSize)                                                                \
    {                                                                                        \
        SMOOTH_PER_TOKEN_SCALED_QUANT_KERNEL_IMPL_(quant_kernel, DTYPE_O, 8, BlockSize)      \
    }                                                                                        \
    else if(cols <= 16 * BlockSize)                                                          \
    {                                                                                        \
        SMOOTH_PER_TOKEN_SCALED_QUANT_KERNEL_IMPL_(quant_kernel, DTYPE_O, 16, BlockSize)     \
    }                                                                                        \
    else if(cols <= 16 * BlockSize * 2)                                                      \
    {                                                                                        \
        SMOOTH_PER_TOKEN_SCALED_QUANT_KERNEL_IMPL_(quant_kernel, DTYPE_O, 16, BlockSize * 2) \
    }                                                                                        \
    else                                                                                     \
    {                                                                                        \
        AITER_CHECK(false, "input last dim has exceeded the maximum value ", 32 * BlockSize); \
    }

void smooth_per_token_scaled_quant(
    aiter_tensor_t& out,         // [..., d]
    const aiter_tensor_t& input, // [..., d]
    aiter_tensor_t& scales,
    const aiter_tensor_t& smooth_scale,
    std::optional<aiter_tensor_t> smooth_scale_map,
    bool shuffle_scale,
    std::optional<aiter_tensor_t> num_rows,
    int num_rows_factor,
    std::optional<aiter_tensor_t> smooth_scale_map_hash,
    bool enable_ps)
{

    int const cols        = input.size(-1);
    int const rows        = input.numel() / cols;
    int32_t* num_rows_ptr = num_rows.has_value() ? reinterpret_cast<int32_t*>(num_rows->data_ptr()) : nullptr;
    int32_t* smooth_scale_map_ptr =
        smooth_scale_map.has_value() ? reinterpret_cast<int32_t*>(smooth_scale_map->data_ptr()) : nullptr;
    int32_t* smooth_scale_map_hash_ptr =
        smooth_scale_map_hash.has_value() ? reinterpret_cast<int32_t*>(smooth_scale_map_hash->data_ptr()) : nullptr;
    AITER_CHECK(
        input.dim() < 4, __func__, " only support input dim <=3, but get dim: ", input.dim());
    int32_t input_dim0    = input.size(0);
    int32_t input_dim1    = input.dim() > 2 ? input.size(1) : 1;
    int32_t input_stride0 = input.stride(0);
    int32_t input_stride1 = input.dim() > 2 ? input.stride(1) : cols;
    int32_t out_dim0 = out.size(0);
    int32_t out_dim1 = out.dim() > 2 ? out.size(1) : 1;
    int32_t out_stride0 = out.stride(0);
    int32_t out_stride1 = out.dim() > 2 ? out.stride(1) : cols;
    int32_t input_stride0_cols = input_stride0 / cols;
    int32_t input_stride1_cols = input_stride1 / cols;
    int32_t out_stride0_cols = out_stride0 / cols;
    int32_t out_stride1_cols = out_stride1 / cols;
    constexpr int32_t MAX_EXPERT_SIZE = 1024;
    int32_t smooth_scale_map_hash_size =
        smooth_scale_map_hash.has_value() ? smooth_scale_map_hash->numel() : 0;
    AITER_CHECK(
        smooth_scale_map_hash_size <= MAX_EXPERT_SIZE, __func__, " smooth_scale_map_hash_size is too large, only support <= ", MAX_EXPERT_SIZE);
    AITER_CHECK((input_dim0 * input_dim1 == out_dim0 * out_dim1) && (input_dim0 == out_dim0 || input_dim0 == out_dim1),
        __func__, "This kernel view input as 3D (m,k,n) and output as 3D (m,k,n)/(k,m,n)");
    const bool transpose_out_dim01 = input_dim0 != out_dim0;

    HipDeviceGuard device_guard(input.device_id);
    const hipStream_t stream = aiter::getCurrentHIPStream();

    if(out.dtype() == AITER_DTYPE_fp8)
    {
        SMOOTH_PER_TOKEN_SCALED_QUANT_KERNEL_DISPATCH(
            smooth_per_token_scaled_quant_kernel, opus::fp8_t, cols);
    }
    else if(out.dtype() == AITER_DTYPE_i8)
    {
        SMOOTH_PER_TOKEN_SCALED_QUANT_KERNEL_DISPATCH(
            smooth_per_token_scaled_quant_kernel, opus::i8_t, cols);
    }
#if defined(__Float4_e2m1fn_x2)
    else if(out.dtype() == AITER_DTYPE_fp4x2 || out.dtype() == AITER_DTYPE_u8)
    {
        SMOOTH_PER_TOKEN_SCALED_QUANT_KERNEL_DISPATCH(
            smooth_per_token_scaled_quant_kernel, opus::fp4_t, cols);
    }
#endif
    else
    {
        AITER_CHECK(false, __func__, " not support output type: ", AiterDtype_to_str(out.dtype()));
    }
}

template <typename DTYPE, int BLOCK_SIZE = 256, int thread_data_size = 4, int MAX_ITERS = 10000>
__global__ void partial_transpose_kernel(DTYPE* __restrict__ out,
                                         DTYPE* __restrict__ input,
                                         const int* __restrict__ num_rows,
                                         const int cols)
{
    using vec_i                     = opus::vector_t<DTYPE, thread_data_size>;
    int GRID_SIZE                   = gridDim.x;
    int ori_rows                    = *num_rows;
    int thread_per_row              = (cols + thread_data_size - 1) / thread_data_size;
    auto const* ptr_i               = reinterpret_cast<DTYPE const*>(input);
    static constexpr int32_t ooba_i = 4 / sizeof(DTYPE);
    const int32_t oob_i             = (ori_rows * cols + ooba_i - 1) / ooba_i * ooba_i;
    static constexpr int32_t load_chunk_bytes = sizeof(DTYPE) * thread_data_size % 16 == 0 ? 16 : (sizeof(DTYPE) * thread_data_size % 8 == 0 ? 8 : 4);
    auto buffer_i = opus::make_gmem<DTYPE>(ptr_i, oob_i * sizeof(DTYPE));
    for(int i = 0; i < MAX_ITERS; i++)
    {
        int64_t y = i * GRID_SIZE * BLOCK_SIZE + blockIdx.x * BLOCK_SIZE + threadIdx.x;
        int x     = y % thread_per_row * thread_data_size;
        y         = y / thread_per_row;
        if(y >= ori_rows)
            return;
        vec_i input_vecs   = load_vector_nbytes<DTYPE, thread_data_size, load_chunk_bytes>(buffer_i, y * cols + x);
        int64_t out_offset = x * ori_rows + y;
        // printf("blockIdx: %d, threadIdx:%d, y: %d, x: %d, ori_rows: %d, cols: %d, val:%f\n",
        // blockIdx.x, threadIdx.x, y, x, ori_rows, cols,
        // static_cast<float>(input_vecs[0]));
        for(int j = 0; j < thread_data_size; j++)
        {
            if((x + j) < cols)
            {
                out[out_offset + j * ori_rows] = input_vecs[j];
            }
        }
    }
}

void partial_transpose(aiter_tensor_t& out,         // [rows, d]
                       const aiter_tensor_t& input, // [rows, d]
                       const aiter_tensor_t& num_rows)
{
    AITER_CHECK(out.is_contiguous());
    AITER_CHECK(input.is_contiguous());

    uint32_t num_cu       = get_num_cu_func();
    int const cols        = input.size(-1);
    int const rows        = input.numel() / cols;
    int32_t* num_rows_ptr = reinterpret_cast<int32_t*>(num_rows.data_ptr());

    HipDeviceGuard device_guard(input.device_id);
    const hipStream_t stream = aiter::getCurrentHIPStream();

    if(cols <= 1024)
    {
        const int BlockSize        = 256;
        const int GridSize         = num_cu * 8; // Adjust as needed
        const int thread_data_size = 1024 / BlockSize;

        dim3 grid(GridSize);
        dim3 block(BlockSize);

        VLLM_DISPATCH_FLOATING_TYPES_rmTorch(input.dtype(), "partial_transpose_kernel", [&] {
            using input_dtype = typename aiter::hip2opus<scalar_t>::type;
            aiter::partial_transpose_kernel<input_dtype, BlockSize, thread_data_size>
                <<<grid, block, 0, stream>>>(reinterpret_cast<input_dtype*>(out.data_ptr()),
                                             reinterpret_cast<input_dtype*>(input.data_ptr()),
                                             num_rows_ptr,
                                             cols);
        });
    }
    else if(cols <= 2048)
    {
        const int BlockSize        = 256;
        const int GridSize         = num_cu * 4;
        const int thread_data_size = 2048 / BlockSize;

        dim3 grid(GridSize);
        dim3 block(BlockSize);

        VLLM_DISPATCH_FLOATING_TYPES_rmTorch(input.dtype(), "partial_transpose_kernel", [&] {
            using input_dtype = typename aiter::hip2opus<scalar_t>::type;
            aiter::partial_transpose_kernel<input_dtype, BlockSize, thread_data_size>
                <<<grid, block, 0, stream>>>(reinterpret_cast<input_dtype*>(out.data_ptr()),
                                             reinterpret_cast<input_dtype*>(input.data_ptr()),
                                             num_rows_ptr,
                                             cols);
        });
    }
    else if(cols <= 4096)
    {
        const int BlockSize        = 256;
        const int GridSize         = num_cu * 2;
        const int thread_data_size = 4096 / BlockSize;

        dim3 grid(GridSize);
        dim3 block(BlockSize);

        VLLM_DISPATCH_FLOATING_TYPES_rmTorch(input.dtype(), "partial_transpose_kernel", [&] {
            using input_dtype = typename aiter::hip2opus<scalar_t>::type;
            aiter::partial_transpose_kernel<input_dtype, BlockSize, thread_data_size>
                <<<grid, block, 0, stream>>>(reinterpret_cast<input_dtype*>(out.data_ptr()),
                                             reinterpret_cast<input_dtype*>(input.data_ptr()),
                                             num_rows_ptr,
                                             cols);
        });
    }
    else if(cols <= 8192)
    {
        const int BlockSize        = 512;
        const int GridSize         = num_cu;
        const int thread_data_size = 8192 / BlockSize;

        dim3 grid(GridSize);
        dim3 block(BlockSize);

        VLLM_DISPATCH_FLOATING_TYPES_rmTorch(input.dtype(), "partial_transpose_kernel", [&] {
            using input_dtype = typename aiter::hip2opus<scalar_t>::type;
            aiter::partial_transpose_kernel<input_dtype, BlockSize, thread_data_size>
                <<<grid, block, 0, stream>>>(reinterpret_cast<input_dtype*>(out.data_ptr()),
                                             reinterpret_cast<input_dtype*>(input.data_ptr()),
                                             num_rows_ptr,
                                             cols);
        });
    }
    else
    {
        AITER_CHECK(false, __func__, " cols is not supported: ", cols);
    }
}


template <typename DTYPE_I, typename DTYPE_O, int block_size, int thread_data_size = 16, bool transpose_out_dim01 = false, bool has_smscale_hash = false, int max_smscale_map_hash_size = 1024>
__global__ void moe_smooth_per_token_scaled_quant_kernel_v1(DTYPE_O* __restrict__ out,
                                                     float* __restrict__ scale,
                                                     DTYPE_I* __restrict__ input,
                                                     float* __restrict__ smooth_scale,
                                                     int* __restrict__ smooth_scale_map,
                                                     int* __restrict__ smooth_scale_map_hash,
                                                     const int32_t num_rows,
                                                     const int32_t m_repeat,
                                                     const int32_t cols,
                                                     const int32_t input_stride     = 1,
                                                     const int32_t smooth_scale_map_hash_size = 256)
{
    __shared__ int32_t smooth_scale_map_hash_shared[1024];
    int token_idx = blockIdx.x;
    int lane_idx = threadIdx.x % WARP_SIZE;
    static constexpr int32_t vec_size_i =
        thread_data_size == 0 ? 16 / sizeof(DTYPE_I) : thread_data_size;
    static constexpr int32_t load_chunk_bytes = 
        (sizeof(DTYPE_I) * vec_size_i % 16 == 0 ? 16 : (sizeof(DTYPE_I) * vec_size_i % 8 == 0 ? 8 : 4));
    if constexpr(has_smscale_hash)
    {
        auto buffer_hash = opus::make_gmem<int>(smooth_scale_map_hash, smooth_scale_map_hash_size * sizeof(int));
        constexpr int32_t async_load_num = (max_smscale_map_hash_size + block_size - 1) / block_size;
        static_assert(max_smscale_map_hash_size <= 1024, "max_smscale_map_hash_size must be less than 1024");
        #pragma unroll
        for(int i = 0; i < async_load_num; i++)
        {
#if defined(__GFX9__)
            const int lds_ptr_sgpr = __builtin_amdgcn_readfirstlane((reinterpret_cast<uintptr_t>((smooth_scale_map_hash_shared + threadIdx.x / WARP_SIZE * WARP_SIZE + i * block_size))));
            uint32_t offset = threadIdx.x * sizeof(int) + i * block_size * sizeof(int);
            asm volatile( "s_mov_b32 m0 %0\n\t"
                "buffer_load_dword %1, %2, 0 offen offset:0 lds\n\t"
                ::"s"(lds_ptr_sgpr), "v"(offset), "s"(buffer_hash.cached_rsrc): "memory", "m0");
#else
            buffer_hash.async_load(smooth_scale_map_hash_shared + threadIdx.x + i * block_size, threadIdx.x + i * block_size);
#endif
        }
    }
    int smscale_map_idx_list = 0;
    auto buffer_map = opus::make_gmem<int>(smooth_scale_map + token_idx * m_repeat, m_repeat * sizeof(int));
    smscale_map_idx_list = buffer_map.load(lane_idx)[0];
    using vec_i = opus::vector_t<DTYPE_I, vec_size_i>;
    using vec_f = opus::vector_t<float, vec_size_i>;
    vec_f vec_input_f;
    float* input_f_ptr = reinterpret_cast<float*>(&vec_input_f);
    auto buffer_input = opus::make_gmem<DTYPE_I>(input + (int64_t)token_idx * (int64_t)input_stride, cols * sizeof(DTYPE_I));
    vec_i vec_input = load_vector_nbytes<DTYPE_I, vec_size_i, load_chunk_bytes, RT>(buffer_input, threadIdx.x * vec_size_i);
#if defined(__gfx1250__)
    opus::s_wait_loadcnt(opus::number<vec_size_i * sizeof(DTYPE_I) / load_chunk_bytes>{});
#else
    opus::s_waitcnt_vmcnt(opus::number<vec_size_i * sizeof(DTYPE_I) / load_chunk_bytes>{});
#endif
    __syncthreads();
    if constexpr(has_smscale_hash)
    {
        if(lane_idx < m_repeat && smscale_map_idx_list >= 0 && smscale_map_idx_list < smooth_scale_map_hash_size)
        {
            smscale_map_idx_list = smooth_scale_map_hash_shared[smscale_map_idx_list];
        }
    }
    for(int i = 0; i < vec_size_i; i++)
    {
        vec_input_f[i] = static_cast<float>(vec_input[i]);
    }
    for(int i = 0; i < m_repeat; i++)
    {
        int32_t smscale_map_idx = __builtin_amdgcn_readlane(smscale_map_idx_list, i);
        if(smscale_map_idx < 0)
        {
            continue;
        }
        auto res = smooth_data_to_per_row_scale<float, DTYPE_O, block_size, thread_data_size>(
            input_f_ptr, smooth_scale, smscale_map_idx, cols);
        float row_scale = std::get<0>(res);
        float* vec_ptr  = std::get<1>(res);

        int out_token_idx;
        if constexpr(transpose_out_dim01)
        {   
            out_token_idx = i * num_rows + token_idx;
        }
        else
        {
            out_token_idx = token_idx * m_repeat + i;
        }
        if(threadIdx.x == 0)
        {
            if constexpr(std::is_same_v<DTYPE_O, opus::fp4_t>)
            {
                auto* tmp        = reinterpret_cast<uint8_t*>(scale);
                uint8_t exponent = (__builtin_bit_cast(uint32_t, row_scale) >> 23) & 0b11111111;
                tmp[out_token_idx]   = exponent;
            }
            else
            {
                scale[out_token_idx] = row_scale;
            }
        }

        int64_t out_offset = (int64_t)out_token_idx * (int64_t)cols;    
        scaled_quant_vgpr_impl<float, DTYPE_O, thread_data_size>(out, vec_ptr, &row_scale, cols, out_offset);
    }
}


#define MOE_SMOOTH_PER_TOKEN_SCALED_QUANT_KERNEL_V1_IMPL(quant_kernel, DTYPE_O, THREAD_DATA, BLOCK_SIZE, TRANSPOSE_OUT_DIM01, HAS_HASH) \
    AITER_DISPATCH_FLOATING16_TYPES_rmTorch(input.dtype(), "quant_kernel", [&] {                                       \
        using input_dtype = typename aiter::hip2opus<scalar_t>::type;                                                  \
        int grid_size = rows;                                                                                          \
        dim3 const grid(grid_size);                                                                                    \
        aiter::quant_kernel<input_dtype, DTYPE_O, BLOCK_SIZE, THREAD_DATA, TRANSPOSE_OUT_DIM01, HAS_HASH, MAX_EXPERT_SIZE> \
            <<<grid, dim3(BLOCK_SIZE), 0, stream>>>(                                                                   \
                reinterpret_cast<DTYPE_O*>(out.data_ptr()),                                                            \
                reinterpret_cast<float*>(scales.data_ptr()),                                                           \
                reinterpret_cast<input_dtype*>(input.data_ptr()),                                                      \
                reinterpret_cast<float*>(smooth_scale.data_ptr()),                                                     \
                smooth_scale_map_ptr,                                                                                  \
                smooth_scale_map_hash_ptr,                                                                             \
                rows,                                                                                                  \
                m_repeat,                                                                                              \
                cols,                                                                                                  \
                input_stride,                                                                                          \
                smooth_scale_map_hash_size);                                                                           \
    });


#define MOE_SMOOTH_PER_TOKEN_SCALED_QUANT_KERNEL_V1_IMPL_(quant_kernel, DTYPE_O, THREAD_DATA, BLOCK_SIZE)                             \
    if(transpose_out_dim01)                                                                                                    \
    {                                                                                                                          \
        if(smooth_scale_map_hash_ptr != nullptr)                                                                               \
            MOE_SMOOTH_PER_TOKEN_SCALED_QUANT_KERNEL_V1_IMPL(quant_kernel, DTYPE_O, THREAD_DATA, BLOCK_SIZE, true, true)       \
        else                                                                                                                   \
            MOE_SMOOTH_PER_TOKEN_SCALED_QUANT_KERNEL_V1_IMPL(quant_kernel, DTYPE_O, THREAD_DATA, BLOCK_SIZE, true, false)      \
    }                                                                                                                          \
    else                                                                                                                       \
    {                                                                                                                          \
        if(smooth_scale_map_hash_ptr != nullptr)                                                                               \
            MOE_SMOOTH_PER_TOKEN_SCALED_QUANT_KERNEL_V1_IMPL(quant_kernel, DTYPE_O, THREAD_DATA, BLOCK_SIZE, false, true)      \
        else                                                                                                                   \
            MOE_SMOOTH_PER_TOKEN_SCALED_QUANT_KERNEL_V1_IMPL(quant_kernel, DTYPE_O, THREAD_DATA, BLOCK_SIZE, false, false)     \
    }

#define MOE_SMOOTH_PER_TOKEN_SCALED_QUANT_KERNEL_V1_DISPATCH(quant_kernel, DTYPE_O, cols)           \
    if(cols <= 4 * BlockSize)                                                                \
    {                                                                                        \
        MOE_SMOOTH_PER_TOKEN_SCALED_QUANT_KERNEL_V1_IMPL_(quant_kernel, DTYPE_O, 8, BlockSize /2)      \
    }                                                                                        \
    else if(cols <= 8 * BlockSize)                                                                \
    {                                                                                        \
        MOE_SMOOTH_PER_TOKEN_SCALED_QUANT_KERNEL_V1_IMPL_(quant_kernel, DTYPE_O, 8, BlockSize)      \
    }                                                                                        \
    else if(cols <= 16 * BlockSize)                                                          \
    {                                                                                        \
        MOE_SMOOTH_PER_TOKEN_SCALED_QUANT_KERNEL_V1_IMPL_(quant_kernel, DTYPE_O, 16, BlockSize)     \
    }                                                                                        \
    else if(cols <= 16 * BlockSize * 2)                                                      \
    {                                                                                        \
        MOE_SMOOTH_PER_TOKEN_SCALED_QUANT_KERNEL_V1_IMPL_(quant_kernel, DTYPE_O, 16, BlockSize * 2) \
    }                                                                                        \
    else                                                                                     \
    {                                                                                        \
        AITER_CHECK(false, "input last dim has exceeded the maximum value ", 32 * BlockSize); \
    }

void moe_smooth_per_token_scaled_quant_v1(
    aiter_tensor_t& out,         // [..., d]
    const aiter_tensor_t& input, // [..., d]
    aiter_tensor_t& scales,
    const aiter_tensor_t& smooth_scale,
    const aiter_tensor_t& smooth_scale_map, // topk_ids
    bool shuffle_scale,
    std::optional<aiter_tensor_t> smooth_scale_map_hash,
    bool transpose_out)
{
    int const cols        = input.size(-1);
    int const rows        = input.numel() / cols;
    int32_t* smooth_scale_map_ptr = reinterpret_cast<int32_t*>(smooth_scale_map.data_ptr());
    int32_t* smooth_scale_map_hash_ptr =
        smooth_scale_map_hash.has_value() ? reinterpret_cast<int32_t*>(smooth_scale_map_hash->data_ptr()) : nullptr;
    int m_repeat = out.numel() / (rows * cols);
    int32_t input_stride = input.stride(-2);
    constexpr int32_t MAX_EXPERT_SIZE = 1024;
    int32_t smooth_scale_map_hash_size =
        smooth_scale_map_hash.has_value() ? smooth_scale_map_hash->numel() : 0;
    AITER_CHECK(out.is_contiguous(), __func__, " out is not contiguous");
    AITER_CHECK(
        smooth_scale_map_hash_size <= MAX_EXPERT_SIZE, __func__, " smooth_scale_map_hash_size is too large, only support <= ", MAX_EXPERT_SIZE);
    const bool transpose_out_dim01 = transpose_out;

    HipDeviceGuard device_guard(input.device_id);
    const hipStream_t stream = aiter::getCurrentHIPStream();

    if(out.dtype() == AITER_DTYPE_fp8)
    {
        MOE_SMOOTH_PER_TOKEN_SCALED_QUANT_KERNEL_V1_DISPATCH(
            moe_smooth_per_token_scaled_quant_kernel_v1, opus::fp8_t, cols);
    }
    else if(out.dtype() == AITER_DTYPE_i8)
    {
        MOE_SMOOTH_PER_TOKEN_SCALED_QUANT_KERNEL_V1_DISPATCH(
            moe_smooth_per_token_scaled_quant_kernel_v1, opus::i8_t, cols);
    }
#if defined(__Float4_e2m1fn_x2)
    else if(out.dtype() == AITER_DTYPE_fp4x2 || out.dtype() == AITER_DTYPE_u8)
    {
        MOE_SMOOTH_PER_TOKEN_SCALED_QUANT_KERNEL_V1_DISPATCH(
            moe_smooth_per_token_scaled_quant_kernel_v1, opus::fp4_t, cols);
    }
#endif
    else
    {
        AITER_CHECK(false, __func__, " not support output type: ", AiterDtype_to_str(out.dtype()));
    }
}


template <typename DTYPE_I, typename DTYPE_O, int block_size, int thread_data_size = 16>
__global__ void moe_smooth_per_token_scaled_quant_kernel_v2(DTYPE_O* __restrict__ out,
                                                            float* __restrict__ scale,
                                                            DTYPE_I* __restrict__ input,
                                                            float* __restrict__ smooth_scale,
                                                            int* __restrict__ sorted_token_ids,
                                                            int* __restrict__ sorted_expert_ids,
                                                            int* __restrict__ num_valid_ids,
                                                            const int32_t num_experts,
                                                            const int32_t num_tokens,
                                                            const int32_t num_blocks,
                                                            const int32_t num_tg,
                                                            const int32_t cols,
                                                            const int32_t topk,
                                                            const int32_t block_m,
                                                            const int32_t block_m_log2split,
                                                            const int32_t input_stride0,
                                                            const int32_t input_stride1,
                                                            const bool shuffle_scale,
                                                            const bool transpose_out_dim01)
{
    int num_valid_ids_value = num_valid_ids[0];
    int block_idx = blockIdx.x;
    const int32_t sub_block_m = block_m >> block_m_log2split;
    for(; block_idx < num_blocks; block_idx += num_tg)
    {
        int sorted_ids_offset = block_idx * sub_block_m;
        if (sorted_ids_offset >= num_valid_ids_value)
        {
            return;
        }
        int lane_idx = threadIdx.x % WARP_SIZE;
        static constexpr int32_t vec_size_i =
            thread_data_size == 0 ? 16 / sizeof(DTYPE_I) : thread_data_size;
        static constexpr int32_t load_chunk_bytes =
            (sizeof(DTYPE_I) * vec_size_i % 16 == 0 ? 16 : (sizeof(DTYPE_I) * vec_size_i % 8 == 0 ? 8 : 4));
        auto buffer_token_ids = opus::make_gmem<int>(sorted_token_ids + sorted_ids_offset, sub_block_m * sizeof(int));
        int token_id_info_list = buffer_token_ids.load(lane_idx)[0];
        int expert_id = sorted_expert_ids[block_idx >> block_m_log2split];
        if (expert_id >= num_experts)
        {
            return;
        }
        using vec_i = opus::vector_t<DTYPE_I, vec_size_i>;
        using vec_f = opus::vector_t<float, vec_size_i>;
        const float inverted_DTYPE_MAX =
            (1. / static_cast<float>(opus::finfo<DTYPE_O>::max()));
        auto buffer_smscale = opus::make_gmem<float>(smooth_scale + expert_id * cols, cols * sizeof(float));
        vec_f smscale = load_vector_nbytes<float, thread_data_size, 16>(buffer_smscale, threadIdx.x * vec_size_i);
        int token_id_list = token_id_info_list & 0xFFFFFF;
        int topk_id_list = token_id_info_list >> 24;
        for(int i = 0; i < sub_block_m; i++)
        { 
            int token_idx = __builtin_amdgcn_readlane(token_id_list, i);
            int topk_id = __builtin_amdgcn_readlane(topk_id_list, i);
            if(token_idx >= num_tokens)
            {
                break;
            }
            int64_t input_offset = (int64_t)token_idx * (int64_t)input_stride0 + (int64_t)(topk_id * input_stride1);
            auto buffer_input = opus::make_gmem<DTYPE_I>(input + input_offset, cols * sizeof(DTYPE_I));
            vec_i vec_input = load_vector_nbytes<DTYPE_I, vec_size_i, load_chunk_bytes, RT>(buffer_input, threadIdx.x * vec_size_i);
            vec_f vec_input_f;
            float* input_f_ptr = reinterpret_cast<float*>(&vec_input_f);
            for(int i = 0; i < vec_size_i; i++)
            {
                vec_input_f[i] = static_cast<float>(vec_input[i]);
            }
            float absMax = 1e-10f;
            #pragma unroll
            for(int j = 0; j < vec_size_i; j++)
            {
                vec_input_f[j] = vec_input_f[j] * smscale[j];
                absMax         = max(absMax, abs(vec_input_f[j]));
            }
            absMax = block_reduce<float, aiter::Max, block_size, true>(absMax, aiter::Max());

            float row_scale = std::is_same_v<DTYPE_O, opus::fp4_t>
                                ? aiter::fp4_f32_to_e8m0_scale(absMax)
                                : absMax * inverted_DTYPE_MAX;
            
            int out_token_idx;
            if (transpose_out_dim01)
            {   
                out_token_idx = topk_id * num_tokens + token_idx;
            }
            else
            {
                out_token_idx = token_idx * topk + topk_id;
            }
            if(threadIdx.x == 0)
            {
                if constexpr(std::is_same_v<DTYPE_O, opus::fp4_t>)
                {
                    auto* tmp        = reinterpret_cast<uint8_t*>(scale);
                    uint8_t exponent = (__builtin_bit_cast(uint32_t, row_scale) >> 23) & 0b11111111;
                    tmp[out_token_idx]   = exponent;
                }
                else
                {
                    scale[out_token_idx] = row_scale;
                }
            }
            int64_t out_offset = (int64_t)out_token_idx * (int64_t)cols;    
            scaled_quant_vgpr_impl<float, DTYPE_O, thread_data_size>(out, input_f_ptr, &row_scale, cols, out_offset);
        }
    }
}


#define MOE_SMOOTH_PER_TOKEN_SCALED_QUANT_KERNEL_V2_IMPL(quant_kernel, DTYPE_O, THREAD_DATA, BLOCK_SIZE)  \
    AITER_DISPATCH_FLOATING16_TYPES_rmTorch(input.dtype(), "quant_kernel", [&] {                           \
        using input_dtype = typename aiter::hip2opus<scalar_t>::type;                                      \
        int blocks_per_cu = 8 * 4 / (BLOCK_SIZE / WARP_SIZE);                                             \
        int num_tg = persistent_mode ? num_cu * blocks_per_cu : num_blocks;                               \
        dim3 const grid(num_tg);                                                                          \
        aiter::quant_kernel<input_dtype, DTYPE_O, BLOCK_SIZE, THREAD_DATA>                                \
            <<<grid, dim3(BLOCK_SIZE), 0, stream>>>(                                                      \
                reinterpret_cast<DTYPE_O*>(out.data_ptr()),                                               \
                reinterpret_cast<float*>(scales.data_ptr()),                                              \
                reinterpret_cast<input_dtype*>(input.data_ptr()),                                         \
                reinterpret_cast<float*>(smooth_scale.data_ptr()),                                        \
                reinterpret_cast<int*>(sorted_token_ids.data_ptr()),                                      \
                reinterpret_cast<int*>(sorted_expert_ids.data_ptr()),                                     \
                reinterpret_cast<int*>(num_valid_ids.data_ptr()),                                         \
                num_experts,                                                                              \
                num_tokens,                                                                               \
                num_blocks,                                                                               \
                num_tg,                                                                                   \
                cols,                                                                                     \
                topk,                                                                                     \
                block_m,                                                                                  \
                block_m_log2split,                                                                        \
                input_stride0,                                                                            \
                input_stride1,                                                                            \
                shuffle_scale,                                                                            \
                transpose_out);                                                                           \
    });


#define MOE_SMOOTH_PER_TOKEN_SCALED_QUANT_KERNEL_V2_DISPATCH(quant_kernel, DTYPE_O, cols)           \
    if(cols <= 4 * BlockSize)                                                                \
    {                                                                                        \
        MOE_SMOOTH_PER_TOKEN_SCALED_QUANT_KERNEL_V2_IMPL(quant_kernel, DTYPE_O, 8, BlockSize /2)      \
    }                                                                                        \
    else if(cols <= 8 * BlockSize)                                                                \
    {                                                                                        \
        MOE_SMOOTH_PER_TOKEN_SCALED_QUANT_KERNEL_V2_IMPL(quant_kernel, DTYPE_O, 8, BlockSize)      \
    }                                                                                        \
    else if(cols <= 16 * BlockSize)                                                          \
    {                                                                                        \
        MOE_SMOOTH_PER_TOKEN_SCALED_QUANT_KERNEL_V2_IMPL(quant_kernel, DTYPE_O, 16, BlockSize)     \
    }                                                                                        \
    else if(cols <= 16 * BlockSize * 2)                                                      \
    {                                                                                        \
        MOE_SMOOTH_PER_TOKEN_SCALED_QUANT_KERNEL_V2_IMPL(quant_kernel, DTYPE_O, 16, BlockSize * 2) \
    }                                                                                        \
    else                                                                                     \
    {                                                                                        \
        AITER_CHECK(false, "input last dim has exceeded the maximum value ", 32 * BlockSize); \
    }


void moe_smooth_per_token_scaled_quant_v2(
    aiter_tensor_t& out,         // [..., d]
    const aiter_tensor_t& input, // [..., d]
    aiter_tensor_t& scales,
    const aiter_tensor_t& smooth_scale,
    const aiter_tensor_t& sorted_token_ids,
    const aiter_tensor_t& sorted_expert_ids,
    const aiter_tensor_t& num_valid_ids,
    int block_m,
    bool shuffle_scale,
    bool transpose_out)
{
    AITER_CHECK(out.is_contiguous());
    int cols = input.size(-1);
    int num_tokens = input.size(0);
    int num_experts = smooth_scale.size(0);
    int topk = out.numel() / (num_tokens * cols);
    int input_stride0= input.stride(0);
    int input_stride1= input.dim() == 2 ? 0 : input.stride(1);

    const int num_cu = get_num_cu_func();
    int block_split = 16;
    int block_m_log2split = log2(block_split);
    AITER_CHECK(block_m % block_split == 0, __func__, " block_m is not divisible by block_split");
    int sub_block_m = block_m >> block_m_log2split;
    int num_blocks = sorted_expert_ids.size(0) * block_split;
    const bool persistent_mode = true;

    HipDeviceGuard device_guard(input.device_id);
    const hipStream_t stream = aiter::getCurrentHIPStream();

    if(out.dtype() == AITER_DTYPE_fp8)
    {
        MOE_SMOOTH_PER_TOKEN_SCALED_QUANT_KERNEL_V2_DISPATCH(
            moe_smooth_per_token_scaled_quant_kernel_v2, opus::fp8_t, cols);
    }
    else if(out.dtype() == AITER_DTYPE_i8)
    {
        MOE_SMOOTH_PER_TOKEN_SCALED_QUANT_KERNEL_V2_DISPATCH(
            moe_smooth_per_token_scaled_quant_kernel_v2, opus::i8_t, cols);
    }
#if defined(__Float4_e2m1fn_x2)
    else if(out.dtype() == AITER_DTYPE_fp4x2 || out.dtype() == AITER_DTYPE_u8)
    {
        MOE_SMOOTH_PER_TOKEN_SCALED_QUANT_KERNEL_V2_DISPATCH(
            moe_smooth_per_token_scaled_quant_kernel_v2, opus::fp4_t, cols);
    }
#endif
    else
    {
        AITER_CHECK(false, __func__, " not support output type: ", AiterDtype_to_str(out.dtype()));
    }
}


// Fused dynamic MX (fp4 / fp8) quantization + MoE-sort writeback.
// Template parameter DTYPE_O selects the element format (opus::fp4_t for MXFP4,
// opus::fp8_t for MXFP8); both paths emit the same E8M0 scale byte layout via
// `aiter::mx_scale_shuffle_idx`. The legacy kernel name
// `mxfp4_quant_moe_sort_kernel` was misleading because it implied fp4-only
// — the implementation has always been dtype-templated.
template <typename DTYPE_O, int thread_data_size>
__device__ void store_zero_mx_quant_moe_sort_row(
    DTYPE_O* __restrict__ out,
    uint8_t* __restrict__ scale,
    const int sorted_row,
    const int32_t scaleN_pad,
    const int32_t scaleN_valid,
    const int scale_k,
    const int num_thread_per_group,
    const int topk_id,
    const int topk,
    const int64_t offset_base,
    const int cols)
{
    if(threadIdx.x % num_thread_per_group == 0 && scale_k < scaleN_valid)
    {
        int addr    = aiter::mx_scale_shuffle_idx(scaleN_pad, sorted_row, scale_k);
        scale[addr] = 0;
    }
    if(topk_id < topk || topk == 1)
    {
        const int64_t row_bytes = std::is_same_v<DTYPE_O, opus::fp4_t> ? cols / 2 : cols;
        uint8_t* out_u8 = reinterpret_cast<uint8_t*>(out);
        const int64_t row_offset = offset_base * row_bytes;
        auto buffer_o = opus::make_gmem<uint8_t>(out_u8 + row_offset, row_bytes);

        static constexpr int32_t zero_vec_bytes = 16;
        opus::vector_t<uint8_t, zero_vec_bytes> zero_vec;
#pragma unroll
        for(int j = 0; j < zero_vec_bytes; j++)
        {
            zero_vec[j] = 0;
        }

        const int32_t num_vecs = (row_bytes + zero_vec_bytes - 1) / zero_vec_bytes;
        for(int32_t vec_idx = threadIdx.x; vec_idx < num_vecs; vec_idx += blockDim.x)
        {
            buffer_o.template store<zero_vec_bytes>(zero_vec, vec_idx * zero_vec_bytes);
        }
    }
}

template <typename DTYPE_I, typename DTYPE_O, int block_size, int thread_data_size = 16>
__global__ void fused_mx_quant_moe_sort_kernel(
    DTYPE_O* __restrict__ out,
    uint8_t* __restrict__ scale,
    DTYPE_I const* __restrict__ input,
    int32_t const* __restrict__ sorted_ids,
    int32_t const* __restrict__ num_valid_ids,
    float const* __restrict__ sorted_weights,
    const int32_t num_tokens,
    const int32_t cols,
    const int32_t group_size,
    const int32_t tgs_per_block_m,
    const int32_t sub_block_m,
    const int32_t num_blocks,
    const int32_t num_tg,
    const int32_t topk,
    const int32_t input_stride)
{
    int num_thread_per_group = group_size / thread_data_size;
    int num_valid_ids_value  = num_valid_ids[0];
    int block_idx            = blockIdx.x;
    int lane_idx             = threadIdx.x % WARP_SIZE;
    const int scale_k        = threadIdx.x / num_thread_per_group;
    static constexpr int32_t vec_size_i =
        thread_data_size == 0 ? 16 / sizeof(DTYPE_I) : thread_data_size;
    static constexpr int32_t load_chunk_bytes =
        (sizeof(DTYPE_I) * vec_size_i % 16 == 0 ? 16
                                                : (sizeof(DTYPE_I) * vec_size_i % 8 == 0 ? 8 : 4));
    using vec_i = opus::vector_t<DTYPE_I, vec_size_i>;
    using vec_f = opus::vector_t<float, vec_size_i>;
    // Continuous fp32 scale divisor for non-MX dtypes (e.g. int8). MX dtypes
    // (fp4 / fp8) take the e8m0 path via fp_f32_to_e8m0_scale<RoundUp, dtype>
    // below, which returns a pure pow-2 dequant scale directly.
    const float inverted_DTYPE_MAX =
        1.0f / static_cast<float>(opus::finfo<DTYPE_O>::max());

    // HW-native FP8 element dtype: gfx942 ships e4m3fnuz (max_pos=240),
    // gfx950+ ships OCP e4m3fn (max_pos=448). The legacy
    // ``fp_f32_to_e8m0_scale<RoundUp, FP4>(absMax) * 1/floor_pow2(MAX)`` formula here used
    // to over-scale the FP8 working value by ~2x (factor*amax > max_pos),
    // saturating the high tail; emit_mx_e8m0_scale<RoundUp, dtype> picks the
    // correct ``ceil_pow2(amax / max_pos)`` per arch instead.
    constexpr aiter::MxDtype kHwFp8Dtype =
#if defined(__gfx942__)
        aiter::MxDtype::FP8_E4M3_FNUZ;
#else
        aiter::MxDtype::FP8_E4M3;
#endif

    const int32_t scaleN_valid = (cols + group_size - 1) / group_size;
    const int32_t scaleN_pad   = ((scaleN_valid + 7) / 8) * 8;

    for(; block_idx < num_blocks; block_idx += num_tg)
    {
        int sub_idx         = block_idx % tgs_per_block_m;
        int block_m_start   = (block_idx - sub_idx) * sub_block_m;
        int sorted_ids_base = block_m_start + sub_idx;
        if(sorted_ids_base >= num_valid_ids_value)
        {
            return;
        }
        int token_id_info_list;
        if (lane_idx < sub_block_m)
        {
            int strided_idx = sorted_ids_base + lane_idx * tgs_per_block_m;
            token_id_info_list = (strided_idx < num_valid_ids_value)
                ? sorted_ids[strided_idx]
                : num_tokens;
        }
        int token_id_list = token_id_info_list & 0xFFFFFF;
        int topk_id_list  = token_id_info_list >> 24;
        for(int i = 0; i < sub_block_m; i++)
        {
            int token_idx = __builtin_amdgcn_readlane(token_id_list, i);
            int topk_id   = __builtin_amdgcn_readlane(topk_id_list, i);
            if(token_idx >= num_tokens)
            {
                break;
            }

            int64_t offset_base = topk == 1 ? (int64_t)(token_idx) : (int64_t)(token_idx * topk + topk_id);
            const int sorted_row = sorted_ids_base + i * tgs_per_block_m;
            if(sorted_weights != nullptr && sorted_weights[sorted_row] == 0.0f)
            {
                store_zero_mx_quant_moe_sort_row<DTYPE_O, thread_data_size>(
                    out,
                    scale,
                    sorted_row,
                    scaleN_pad,
                    scaleN_valid,
                    scale_k,
                    num_thread_per_group,
                    topk_id,
                    topk,
                    offset_base,
                    cols);
                continue;
            }
            auto buffer_input =
                opus::make_gmem<DTYPE_I>(input + offset_base * input_stride, cols * sizeof(DTYPE_I));
            vec_i vec_input = load_vector_nbytes<DTYPE_I, vec_size_i, load_chunk_bytes, RT>(
                buffer_input, threadIdx.x * vec_size_i);
            vec_f vec_input_f;
            float* input_f_ptr = reinterpret_cast<float*>(&vec_input_f);
            float absMax       = 1e-10f;
            #pragma unroll
            for(int j = 0; j < vec_size_i; j++)
            {
                vec_input_f[j] = static_cast<float>(vec_input[j]);
                absMax         = max(absMax, abs(vec_input_f[j]));
            }
            absMax = multithread_reduce(absMax, aiter::Max(), num_thread_per_group);

            // MXFP4 / MXFP8 use the project-wide default round mode
            // (kDefaultMxScaleRoundMode, currently NV ROUND_UP =
            // ceil_pow2(amax / max_pos)). The helper returns the dequant
            // scale as a pow-2 fp32, so the ``(>> 23) & 0xFF`` extraction
            // below yields the stored e8m0 byte directly. Other dtypes fall
            // back to a continuous fp32 scale.
            float row_scale;
            if constexpr (std::is_same_v<DTYPE_O, opus::fp4_t>)
            {
                row_scale = aiter::fp4_f32_to_e8m0_scale(absMax);
            }
            else if constexpr (std::is_same_v<DTYPE_O, opus::fp8_t>)
            {
                row_scale = aiter::fp_f32_to_e8m0_scale<aiter::kDefaultMxScaleRoundMode,
                                                       kHwFp8Dtype>(absMax);
            }
            else
            {
                row_scale = absMax * inverted_DTYPE_MAX;
            }

            if(threadIdx.x % num_thread_per_group == 0 && scale_k < scaleN_valid)
            {
                uint8_t bs_e8m0 = (__builtin_bit_cast(uint32_t, row_scale) >> 23) & 0xFF;
                int addr        = aiter::mx_scale_shuffle_idx(scaleN_pad, sorted_row, scale_k);
                scale[addr]     = bs_e8m0;
            }

            if(topk_id < topk || topk == 1)
            {
                scaled_quant_vgpr_impl<float, DTYPE_O, thread_data_size>(
                    out, input_f_ptr, &row_scale, cols, offset_base * cols);
            }
        }
    }
}


#define FUSED_MX_QUANT_MOE_SORT_KERNEL_IMPL(DTYPE_O, THREAD_DATA, BLOCK_SIZE)                       \
    AITER_DISPATCH_FLOATING16_TYPES_rmTorch(input.dtype(), "fused_mx_quant_moe_sort_kernel", [&] {  \
        using input_dtype = typename aiter::hip2opus<scalar_t>::type;                               \
        AITER_CHECK(group_size % THREAD_DATA == 0, __func__, " group_size is not divisible by THREAD_DATA"); \
        int blocks_per_cu = 8 * 4 / (BLOCK_SIZE / WARP_SIZE);                                       \
        int num_tg = persistent_mode ? num_cu * blocks_per_cu : num_blocks;                         \
        dim3 const grid(num_tg);                                                                    \
        fused_mx_quant_moe_sort_kernel<input_dtype, DTYPE_O, BLOCK_SIZE, THREAD_DATA>               \
            <<<grid, dim3(BLOCK_SIZE), 0, stream>>>(                                           \
                reinterpret_cast<DTYPE_O*>(output.data_ptr()),                                  \
                reinterpret_cast<uint8_t*>(scale.data_ptr()),                                   \
                reinterpret_cast<input_dtype const*>(input.data_ptr()),                         \
                reinterpret_cast<int32_t*>(sorted_ids.data_ptr()),                              \
                reinterpret_cast<int32_t*>(num_valid_ids.data_ptr()),                           \
                sorted_weights_ptr,                                                            \
                token_num,                                                                      \
                cols,                                                                           \
                group_size,                                                                     \
                tgs_per_block_m,                                                                \
                sub_block_m,                                                                     \
                num_blocks,                                                                     \
                num_tg,                                                                         \
                topk,                                                                           \
                input_stride);                                                                  \
    });


#define FUSED_MX_QUANT_MOE_SORT_KERNEL_DISPATCH(DTYPE_O, cols_)                                \
    if(cols_ <= 2 * BlockSize)                                                                 \
    {                                                                                          \
        FUSED_MX_QUANT_MOE_SORT_KERNEL_IMPL(DTYPE_O, 8, BlockSize / 4)                         \
    }                                                                                          \
    else if(cols_ <= 4 * BlockSize)                                                            \
    {                                                                                          \
        FUSED_MX_QUANT_MOE_SORT_KERNEL_IMPL(DTYPE_O, 8, BlockSize / 2)                         \
    }                                                                                          \
    else if(cols_ <= 8 * BlockSize)                                                            \
    {                                                                                          \
        FUSED_MX_QUANT_MOE_SORT_KERNEL_IMPL(DTYPE_O, 8, BlockSize)                             \
    }                                                                                          \
    else if(cols_ <= 16 * BlockSize)                                                           \
    {                                                                                          \
        FUSED_MX_QUANT_MOE_SORT_KERNEL_IMPL(DTYPE_O, 16, BlockSize)                            \
    }                                                                                          \
    else if(cols_ <= 16 * BlockSize * 2)                                                       \
    {                                                                                          \
        FUSED_MX_QUANT_MOE_SORT_KERNEL_IMPL(DTYPE_O, 32, BlockSize)                            \
    }                                                                                          \
    else                                                                                       \
    {                                                                                          \
        AITER_CHECK(false, "input last dim has exceeded the maximum value ", 32 * BlockSize);  \
    }

static void fused_dynamic_mx_quant_moe_sort_hip_impl(aiter_tensor_t& output,
                                                     aiter_tensor_t& scale,
                                                     const aiter_tensor_t& input,
                                                     const aiter_tensor_t& sorted_ids,
                                                     const aiter_tensor_t& num_valid_ids,
                                                     int token_num,
                                                     int block_m,
                                                     int group_size,
                                                     std::optional<aiter_tensor_t> sorted_weights,
                                                     int64_t padded_rows_upper_bound)
{
    int cols = input.size(-1);
    int topk = input.numel() / (cols * token_num);
    int num_experts = (sorted_ids.size(0) + topk - topk * token_num) / block_m;
    if(sorted_weights.has_value())
    {
        AITER_CHECK(sorted_weights->dtype() == AITER_DTYPE_fp32,
                    __func__,
                    " sorted_weights must be fp32 when provided");
        AITER_CHECK(sorted_weights->numel() >= sorted_ids.size(0),
                    __func__,
                    " sorted_weights must have at least sorted_ids.size(0) elements");
    }
    float const* sorted_weights_ptr =
        sorted_weights.has_value() ? reinterpret_cast<float const*>(sorted_weights->data_ptr()) : nullptr;

    const int num_cu = get_num_cu_func();
    int sub_block_m = (token_num * topk) > (num_cu * 8) || num_experts < 64 ? 2 : 4;
    AITER_CHECK(block_m % sub_block_m == 0, __func__, " block_m is not divisible by sub_block_m");
    int tgs_per_block_m = block_m / sub_block_m;
    const int64_t old_extent = sorted_ids.size(0);
    int64_t effective_extent = old_extent;
    if(padded_rows_upper_bound >= 0)
    {
        // An over-conservative host-side expert bound must never enlarge the
        // old launch. This also preserves the legacy extent if the calculated
        // upper bound is greater than the sorted_ids allocation.
        effective_extent =
            padded_rows_upper_bound < old_extent ? padded_rows_upper_bound : old_extent;
    }
    int num_blocks = (effective_extent + sub_block_m - 1) / sub_block_m;
    if(num_blocks == 0 && old_extent > 0)
    {
        // HIP does not accept a zero-sized grid. One workgroup is enough to
        // read num_valid_ids==0 and return; it is no larger than the legacy
        // launch, which also contains at least one workgroup.
        num_blocks = 1;
    }
    const bool persistent_mode = false;
    const int input_stride     = input.stride(-2);

    HipDeviceGuard device_guard(input.device_id);
    const hipStream_t stream = aiter::getCurrentHIPStream();

    if(output.dtype() == AITER_DTYPE_fp8)
    {
        FUSED_MX_QUANT_MOE_SORT_KERNEL_DISPATCH(opus::fp8_t, cols);
    }
#if defined(__Float4_e2m1fn_x2)
    else if(output.dtype() == AITER_DTYPE_fp4x2 || output.dtype() == AITER_DTYPE_u8)
    {
        FUSED_MX_QUANT_MOE_SORT_KERNEL_DISPATCH(opus::fp4_t, cols);
    }
#endif
    else
    {
        AITER_CHECK(false, __func__, ": not support output type: ", AiterDtype_to_str(output.dtype()));
    }
}

void fused_dynamic_mx_quant_moe_sort_hip(aiter_tensor_t& output,
                                         aiter_tensor_t& scale,
                                         const aiter_tensor_t& input,
                                         const aiter_tensor_t& sorted_ids,
                                         const aiter_tensor_t& num_valid_ids,
                                         int token_num,
                                         int block_m,
                                         int group_size,
                                         std::optional<aiter_tensor_t> sorted_weights)
{
    fused_dynamic_mx_quant_moe_sort_hip_impl(output,
                                             scale,
                                             input,
                                             sorted_ids,
                                             num_valid_ids,
                                             token_num,
                                             block_m,
                                             group_size,
                                             sorted_weights,
                                             -1);
}

void fused_dynamic_mx_quant_moe_sort_hip_bounded(aiter_tensor_t& output,
                                                 aiter_tensor_t& scale,
                                                 const aiter_tensor_t& input,
                                                 const aiter_tensor_t& sorted_ids,
                                                 const aiter_tensor_t& num_valid_ids,
                                                 int token_num,
                                                 int block_m,
                                                 int64_t total_routes,
                                                 int64_t num_experts_upper_bound,
                                                 int group_size,
                                                 std::optional<aiter_tensor_t> sorted_weights)
{
    AITER_CHECK(total_routes >= 0, __func__, " total_routes must be non-negative");
    // A non-positive bound clamps the launch extent to 0, silently skipping
    // every row, so reject it instead of under-launching.
    AITER_CHECK(num_experts_upper_bound > 0, __func__, " num_experts_upper_bound must be positive");
    const int64_t padded_rows_upper_bound =
        moe_quant_padded_rows_upper_bound(total_routes, num_experts_upper_bound, block_m);
    fused_dynamic_mx_quant_moe_sort_hip_impl(output,
                                             scale,
                                             input,
                                             sorted_ids,
                                             num_valid_ids,
                                             token_num,
                                             block_m,
                                             group_size,
                                             sorted_weights,
                                             padded_rows_upper_bound);
}

// Perf gate threshold for the coalesced LDS-staged store path in
// mxfp4_moe_sort_kernel (see below). The LDS staging + double __syncthreads
// cost scales with the LDS footprint per row (scaleN_pad bytes) times the
// number of grid tiles (num_blocks). On MI355X/gfx950 uncoalesced byte stores
// are already cheap, so the coalesced path is a net win for small/mid
// (scaleN_pad x num_blocks) but loses once that product is large (empirically
// the only regressor is dim=7168 @ M=16384: 224*4352=974,848; the largest
// winner is dim=7168 @ M=8192: 224*2304=516,096). The threshold sits between
// them with ~35% margin. This is a PURE PERFORMANCE knob: both the coalesced
// and the scatter paths are byte-exact, so the value only affects speed, never
// correctness.
constexpr long MXFP4_MOE_SORT_COALESCED_LDS_WORK_MAX = 700000;

template <int block_size, int num_rows, int thread_data_size = 16, int group_size = 32>
__global__ void mxfp4_moe_sort_kernel(
    uint8_t* __restrict__ out_scale,
    uint8_t* __restrict__ scale,
    int32_t const* __restrict__ sorted_ids,
    int32_t const* __restrict__ num_valid_ids,
    const int32_t num_tokens,
    const int32_t cols,
    const int32_t num_blocks,
    const int32_t num_tg,
    const int32_t topk)
{
    constexpr int threads_per_row = block_size / num_rows;
    int num_valid_ids_value  = num_valid_ids[0];
    int block_idx            = blockIdx.x;
    int row_i                = threadIdx.x / threads_per_row;
    int scale_k              = threadIdx.x % threads_per_row * thread_data_size;
    const int scale_per_row = (cols + group_size - 1) / group_size;
    static constexpr int32_t vec_size_i = thread_data_size;
    static constexpr int32_t load_chunk_bytes =
        (sizeof(uint8_t) * vec_size_i % 16 == 0 ? 16
                                                : (sizeof(uint8_t) * vec_size_i % 8 == 0 ? 8 
                                                : (sizeof(uint8_t) * vec_size_i % 4 == 0 ? 4 : 2)));
    using vec_i = opus::vector_t<uint8_t, vec_size_i>;
    const int32_t scaleN_valid = (cols + group_size - 1) / group_size;
    const int32_t scaleN_pad   = ((scaleN_valid + 7) / 8) * 8;
    auto buffer_scale =
                opus::make_gmem<uint8_t>(scale, scale_per_row * num_tokens * topk * sizeof(uint8_t));

    // Optimized store path: when one threadblock maps to exactly one 32-row
    // swizzle tile (num_rows == 32), we stage this block's scale bytes into
    // LDS in natural [row][col] order, then emit the swizzled out_scale layout
    // with fully-coalesced 4-byte (dword) stores instead of 32 strided 1-byte
    // scatters. The swizzle address for a fixed 32-row tile decomposes so that
    // each contiguous 256-byte region == (one tile) x (8 columns), and one
    // aligned dword at offset (y_blk*256 + c*64 + xl_lo*4) packs exactly:
    //   byte0 = data[xl_lo   ][col_a]   byte1 = data[xl_lo+16][col_a]
    //   byte2 = data[xl_lo   ][col_a+4] byte3 = data[xl_lo+16][col_a+4]
    // with col_a = y_blk*8 + c. So 64 consecutive dwords fill a 256B block.
    //
    // The coalesced path is gated by a pure perf heuristic computed here from
    // values already in scope (scaleN_pad = LDS footprint per row, num_blocks =
    // grid tiles); see MXFP4_MOE_SORT_COALESCED_LDS_WORK_MAX above. When the
    // gate is off we fall through to the original per-byte scatter path below.
    // Both paths are byte-exact; the gate only selects the faster one. The
    // (long) casts avoid int32 overflow of the product.
    if constexpr (num_rows == 32)
    {
      if(((long)scaleN_pad * (long)num_blocks) <= MXFP4_MOE_SORT_COALESCED_LDS_WORK_MAX)
      {
        // LDS holds 32 rows x scaleN_pad bytes (upper bound is compile-time:
        // threads_per_row * thread_data_size columns). Zero-staged so invalid
        // rows and padding columns store 0 (byte-exact with the reference,
        // whose buffer is zero-initialised).
        //
        // The LDS row stride is PADDED (LDS_STRIDE = lds_cols + LDS_PAD), kept
        // separate from the output stride scaleN_pad. Without padding, a
        // scaleN_pad that is a multiple of 32 dwords (e.g. dim=4096 ->
        // scaleN_pad=128 = 32 LDS banks) makes all 32 rows of a tile alias onto
        // the same LDS bank, causing a ~32-way bank conflict in the packed
        // read-out (s_scale[xl_lo*..] and s_scale[(xl_lo+16)*..]). Padding the
        // stride by 4 bytes (one dword) breaks that aliasing while preserving
        // dword alignment of the staged reads. This is invisible to the output:
        // out_scale addressing still uses scaleN_pad, so the result is
        // byte-identical; only the in-LDS layout changes.
        constexpr int lds_cols   = threads_per_row * thread_data_size;
        constexpr int LDS_PAD    = 4;
        constexpr int LDS_STRIDE = lds_cols + LDS_PAD;
        __shared__ uint8_t s_scale[num_rows * LDS_STRIDE];

        for(; block_idx < num_blocks; block_idx += num_tg)
        {
            // Skip tiles whose 32 rows are entirely in the padding region
            // (all rows >= num_valid_ids -> all invalid). The output buffer is
            // pre-zeroed, so leaving such tiles untouched is byte-exact with the
            // reference, and matches the original kernel's per-row guard. This
            // avoids streaming zeros to the large E*block_m padding region.
            // block_idx is uniform across the block, so no __syncthreads hazard.
            if(block_idx * num_rows >= num_valid_ids_value)
            {
                continue;
            }
            int sorted_row = block_idx * num_rows + row_i;
            int token_id_info = num_tokens;
            if (sorted_row < num_valid_ids_value)
            {
                token_id_info = sorted_ids[sorted_row];
            }
            int token_idx = token_id_info & 0xFFFFFF;
            int topk_id   = token_id_info >> 24;
            bool valid = (token_idx < num_tokens && (topk == 1 || topk_id < topk));

            vec_i vec_scale;
            if(valid)
            {
                int64_t scale_offset;
                if (topk == 1)
                {
                    scale_offset = (int64_t)(token_idx) * scale_per_row;
                }
                else
                {
                    scale_offset = (int64_t)(token_idx * topk + topk_id) * scale_per_row;
                }
                vec_scale = load_vector_nbytes<uint8_t, vec_size_i, load_chunk_bytes, RT>(
                    buffer_scale, scale_offset + scale_k);
            }

            __syncthreads();
            for(int j = 0; j < vec_size_i; j++)
            {
                int col = scale_k + j;
                if(col < scaleN_pad)
                {
                    s_scale[row_i * LDS_STRIDE + col] =
                        (valid && col < scaleN_valid) ? vec_scale[j] : (uint8_t)0;
                }
            }
            __syncthreads();

            const int ngroups   = scaleN_pad / 8;
            const int total_dw  = ngroups * 64;
            const int64_t tile_base = (int64_t)block_idx * scaleN_pad * 32;
            for(int d = threadIdx.x; d < total_dw; d += block_size)
            {
                int y_blk  = d >> 6;
                int t      = d & 63;
                int xl_lo  = t & 15;
                int c      = t >> 4;
                int col_a  = y_blk * 8 + c;
                int col_b  = col_a + 4;
                uint32_t b0 = s_scale[xl_lo * LDS_STRIDE + col_a];
                uint32_t b1 = s_scale[(xl_lo + 16) * LDS_STRIDE + col_a];
                uint32_t b2 = s_scale[xl_lo * LDS_STRIDE + col_b];
                uint32_t b3 = s_scale[(xl_lo + 16) * LDS_STRIDE + col_b];
                uint32_t packed = b0 | (b1 << 8) | (b2 << 16) | (b3 << 24);
                int64_t addr = tile_base + (int64_t)y_blk * 256 + (int64_t)c * 64 + xl_lo * 4;
                *reinterpret_cast<uint32_t*>(out_scale + addr) = packed;
            }
        }
        return;
      }
    }

    for(; block_idx < num_blocks; block_idx += num_tg)
    {
        int sorted_row = block_idx * num_rows + row_i;
        int token_id_info = num_tokens;
        if (sorted_row < num_valid_ids_value)
        {
            token_id_info = sorted_ids[sorted_row];
        }
        int token_idx = token_id_info & 0xFFFFFF;
        int topk_id   = token_id_info >> 24;
        if(token_idx < num_tokens && (topk == 1 || topk_id < topk))
        {
            int64_t scale_offset;
            if (topk == 1)
            {
                scale_offset = (int64_t)(token_idx) * scale_per_row;
            }
            else
            {
                scale_offset = (int64_t)(token_idx * topk + topk_id) * scale_per_row;
            }
            vec_i vec_scale = load_vector_nbytes<uint8_t, vec_size_i, load_chunk_bytes, RT>(
                buffer_scale, scale_offset + scale_k);

            for(int j = 0; j < vec_size_i; j++)
            {
                if((scale_k + j) < scaleN_valid)
                {
                    int addr = aiter::mx_scale_shuffle_idx(scaleN_pad, sorted_row, scale_k + j);
                    out_scale[addr] = vec_scale[j];
                }
            }
        }
    }
}


#define MXFP4_MOE_SORT_KERNEL_IMPL(MAX_COL, THREAD_DATA, BLOCK_SIZE)                    \
    constexpr int GROUP_SIZE = 32;                                                      \
    constexpr int NUM_ROWS = BLOCK_SIZE / (MAX_COL /(GROUP_SIZE * THREAD_DATA));        \
    AITER_CHECK(BLOCK_SIZE % (MAX_COL /(GROUP_SIZE * THREAD_DATA)) == 0);               \
    int num_blocks = (sorted_ids.size(0) + NUM_ROWS - 1) / NUM_ROWS;                    \
    int blocks_per_cu = 8 * 4 / (BLOCK_SIZE / WARP_SIZE);                               \
    int num_tg = persistent_mode ? num_cu * blocks_per_cu : num_blocks;                 \
    dim3 const grid(num_tg);                                                            \
    /* The coalesced-store perf gate is computed inside the kernel from        */       \
    /* num_blocks + scaleN_pad (no extra launch arg / signature change).       */       \
    mxfp4_moe_sort_kernel<BLOCK_SIZE, NUM_ROWS, THREAD_DATA, GROUP_SIZE>                \
        <<<grid, dim3(BLOCK_SIZE), 0, stream>>>(                                        \
            reinterpret_cast<uint8_t*>(out_scale.data_ptr()),                           \
            reinterpret_cast<uint8_t*>(scale.data_ptr()),                               \
            reinterpret_cast<int32_t*>(sorted_ids.data_ptr()),                          \
            reinterpret_cast<int32_t*>(num_valid_ids.data_ptr()),                       \
            token_num, cols, num_blocks, num_tg, topk);


#define MXFP4_MOE_SORT_KERNEL_DISPATCH(cols_)                                                  \
    if(cols_ <= 256)                                                                           \
    {                                                                                          \
        MXFP4_MOE_SORT_KERNEL_IMPL(256, 4, 256)                                                \
    }                                                                                          \
    else if(cols_ <= 512)                                                                      \
    {                                                                                          \
        MXFP4_MOE_SORT_KERNEL_IMPL(512, 4, 256)                                                \
    }                                                                                          \
    else if(cols_ <= 1024)                                                                     \
    {                                                                                          \
        MXFP4_MOE_SORT_KERNEL_IMPL(1024, 4, 256)                                               \
    }                                                                                          \
    else if(cols_ <= 2048)                                                                     \
    {                                                                                          \
        MXFP4_MOE_SORT_KERNEL_IMPL(2048, 8, 256)                                               \
    }                                                                                          \
    else if(cols_ <= 4096)                                                                     \
    {                                                                                          \
        MXFP4_MOE_SORT_KERNEL_IMPL(4096, 16, 256)                                              \
    }                                                                                          \
    else if(cols_ <= 6144)                                                                     \
    {                                                                                          \
        MXFP4_MOE_SORT_KERNEL_IMPL(6144, 24, 256)                                              \
    }                                                                                          \
    else if(cols_ <= 8192)                                                                     \
    {                                                                                          \
        MXFP4_MOE_SORT_KERNEL_IMPL(8192, 32, 256)                                              \
    }                                                                                          \
    else if(cols_ <= 16384)                                                                    \
    {                                                                                          \
        MXFP4_MOE_SORT_KERNEL_IMPL(16384, 32, 256)                                             \
    }                                                                                          \
    else                                                                                       \
    {                                                                                          \
        AITER_CHECK(false, "input last dim has exceeded the maximum value ", 16384);            \
    }

void mxfp4_moe_sort_hip(
    aiter_tensor_t& out_scale,
    const aiter_tensor_t& scale,
    const aiter_tensor_t& sorted_ids,
    const aiter_tensor_t& num_valid_ids,
    int token_num,
    int cols
)
{
    const int num_cu = get_num_cu_func();
    const bool persistent_mode = false;
    int topk = scale.numel() / ((cols + 31) / 32 * token_num);

    HipDeviceGuard device_guard(scale.device_id);
    const hipStream_t stream = aiter::getCurrentHIPStream();

    MXFP4_MOE_SORT_KERNEL_DISPATCH(cols);
}

} // namespace aiter